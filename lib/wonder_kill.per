;Rule1: make sure that we have 1 explore group

(defrule
	(strategic-number sn-number-explore-groups != 1)
=>
	(set-strategic-number sn-number-explore-groups 1)
)

(defrule
	(cc-players-building-type-count any-enemy wonder > 0)
=>
	(set-goal WONDER-ATTEMPT ATTACK)
	(disable-self)
)

(defrule
	(or
		(building-type-count wonder > 0)
		(cc-players-building-type-count any-ally wonder > 0)
	)
=>
	(set-goal WONDER-ATTEMPT DEFEND)
	(disable-self)
)



;Rule4: initialisation of the attack timer if we are attacker

(defrule
	(goal WONDER-ATTEMPT ATTACK)
=>
	(enable-timer T-ATTACK 30)
	(disable-self)
)

(defrule
	(goal WONDER-ATTEMPT ATTACK)
	(timer-triggered T-ATTACK)
	(population >= pop-cap)
=>
	(set-goal MILITARY-PARITY YES)
)

(defrule
	(goal WONDER-ATTEMPT ATTACK)
	(timer-triggered T-ATTACK)
	(or
		(military-population >= TWENTY-PERCENT-POP)
		(or
			(and
				(players-military-population any-enemy < FIFTEEN-PERCENT-POP)
				(military-population >= FIFTEEN-PERCENT-POP)
			)
			(and
				(players-military-population any-enemy < TEN-PERCENT-POP)
				(military-population >= TEN-PERCENT-POP)
			)
		)
	)
=>
	(set-goal MILITARY-PARITY YES)
)

(defrule
	(goal MILITARY-PARITY YES)
	(unit-type-count-total trebuchet == 0)
	(unit-type-count-total battering-ram-line == 0)
	(unit-type-count-total mangonel-line == 0)
	(unit-type-count-total bombard-cannon == 0)
	(unit-type-count-total elite-tarkan < 10)
	(unit-type-count-total petard < 3)
	(military-population < TWENTY-PERCENT-POP)
=>
	(set-goal MILITARY-PARITY NO)
)

(defrule
	(taunt-detected any-ally 201)
=>
	(acknowledge-taunt this-any-ally 201)
	(attack-now)
	(chat-local-to-self "attack now!")
	(set-goal MILITARY-PARITY YES)
)

(defrule
	(goal MILITARY-PARITY YES)
=>
	(set-strategic-number sn-percent-attack-soldiers 100)
	(attack-now)
	(chat-to-player-using-id every-ally 22406)
	(chat-to-player every-ally "39 I attack an enemy now!")
	(chat-to-player every-ally "Hopefully you can reinforce my units!")
)

(defrule
	(timer-triggered T-ATTACK)
=>
	(set-goal MILITARY-PARITY NO)
	(disable-timer T-ATTACK)
	(enable-timer T-ATTACK 30)
)

(defrule
	(goal WONDER-ATTEMPT ATTACK)
=>
	(set-strategic-number sn-special-attack-type3 wonder)
	(disable-self)
)
