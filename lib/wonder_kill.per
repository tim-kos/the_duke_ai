;Rule1: make sure that we have 1 explore group

(defrule
	(strategic-number sn-number-explore-groups != 1)
=>
	(set-strategic-number sn-number-explore-groups 1)
)



;Rule2: make sure which side we are (attacker)

(defrule
	(cc-players-building-type-count any-enemy wonder > 0);any enemy has got the wonder
=>
	(set-goal WONDER-ATTEMPT ATTACK)	;the AI sets goal WONDER-ATTEMPT to ATTACK (constant)
	(disable-self)				;only once <- initialisation
)



;Rule3: make sure which side we are (defender)

(defrule
	(or
		(building-type-count wonder > 0)	;we've got the wonder
		(cc-players-building-type-count any-ally wonder > 0)	;or any ally has got the wonder
	)
=>
	(set-goal WONDER-ATTEMPT DEFEND)	;the AI sets goal WONDER-ATTEMPT to DEFEND
	(disable-self)				;only once <0 initialisation
)



;**********************************************************
;DEFEND-THE-WONDER REQUIRES SOME SPECIAL STUFF OF ATTACKING



;Rule4: initialisation of the attack timer if we are attacker

(defrule
	(goal WONDER-ATTEMPT ATTACK)	;goal WONDER-ATTEMPT to ATTACK (constant)
=>
	(enable-timer T-ATTACK 30)		;the AI enables the timer with id T-ATTACK with 30 seconds (constant)
	(disable-self)				;only once
)



;Rule5: set the attack-goal we've arrived on the pop cap

(defrule
	(goal WONDER-ATTEMPT ATTACK)	;goal WONDER-ATTEMPT to ATTACK (constant)
	(timer-triggered T-ATTACK)		;the timer with id T-ATTACK is over
	(population >= pop-cap)			;with arrived at our pop-cap
=>
	(set-goal MILITARY-PARITY YES)	;the AI sets goal MILITARY-PARITY to YES (constant)
)



;Rule6: set the attack-goal if our military is strong unlike the soldiers of our enemies

(defrule
	(goal WONDER-ATTEMPT ATTACK)	;goal WONDER-ATTEMPT to ATTACK (constant)
	(timer-triggered T-ATTACK)		;the timer with id T-ATTACK is over
	(or
		(military-population >= TWENTY-PERCENT-POP);we have got at least TWENTY-PERCENT-POP soldiers (constant)
		(or
			(and
				(players-military-population any-enemy < FIFTEEN-PERCENT-POP);or any enemy has got less than FIFTEEN-PERCENT-POP (constant)
				(military-population >= FIFTEEN-PERCENT-POP);and we've got at least FIFTEEN-PERCENT-POP soldiers (constant)
			)
			(and
				(players-military-population any-enemy < TEN-PERCENT-POP);or any enemy has got less than TEN-PERCENT-POP soldiers (constant)
				(military-population >= TEN-PERCENT-POP);and we've got at least TEN-PERCENT-POP soldiers (constant)
			)
		)
	)
=>
	(set-goal MILITARY-PARITY YES)	;the AI sets goal MILITARY-PARITY to YES (constant)
)



;Rule7: don't attack if we haven't got any siege-unit, it would be stupid, because probably we haven't  got any change to kill the wonder

(defrule
	(goal MILITARY-PARITY YES)	;goal MILITARY-PARITY to YES (constant)
	(unit-type-count-total trebuchet == 0)	;we haven't got any trebuchet
	(unit-type-count-total battering-ram-line == 0);we haven't got any ram
	(unit-type-count-total mangonel-line == 0)	;we haven't got any onager
	(unit-type-count-total bombard-cannon == 0);we haven't got any bombard-cannon
	(unit-type-count-total elite-tarkan < 10)	;we haven't got at least 10 tarkans
	(unit-type-count-total petard < 3)		;number of petards less than 3
	(military-population < TWENTY-PERCENT-POP);number of soldiers less than TWENTY-PERCENT-POP
=>
	(set-goal MILITARY-PARITY NO)	;the AI sets goal MILITARY-PARITY to NO (constant)
)



;Rule8: attack if an ally sent us the tactical command

(defrule
	(taunt-detected any-ally 201)		;the tactical AI sent us taunt with id 201
=>
	(acknowledge-taunt this-any-ally 201)	;the AI acknoldges this taunt (sets the flag back)
	(attack-now)				;the AI attacks an enemy
	(chat-local-to-self "attack now!")		;the AI sends the chat as a local one to itselfs
	(set-goal MILITARY-PARITY YES)	;the AI sets goal MILITARY-PARITY to YES (constant)
)



;Rule9: attack if our military is strong unlike the soldiers of our enemies

(defrule
	(goal MILITARY-PARITY YES)	;goal MILITARY-PARITY to YES (constant)
=>
	(set-strategic-number sn-percent-attack-soldiers 100);the AI attacks with all soldiers
	(attack-now)				;the AI attacks an enemy
	(chat-to-player-using-id every-ally 22406)	;the AI sends this chat to every ally, so that the rule above will become true and the AI that are ally will do a combinated attack
	(chat-to-player every-ally "39 I attack an enemy now!");the AI sends the chat as a local one to all its allies
	(chat-to-player every-ally "Hopefully you can reinforce my units!");the AI sends the chat as a local one to all its allies
)



;Rule10: restart the attack-timer-loop

(defrule
	(timer-triggered T-ATTACK)		;the timer with id T-ATTACK is over (constant)
=>
	(set-goal MILITARY-PARITY NO)	;the AI sets goal MILITARY-PARITY to NO 8constant)
	(disable-timer T-ATTACK)		;the AI disables (sets the flag back) the timer with id T-ATTACK (constant)
	(enable-timer T-ATTACK 30)		;the AI enables timer with id T-ATTACK with 30 seconds 8constant)
)



;Rule11: make sure we only attack wonders (prefered)

(defrule
	(goal WONDER-ATTEMPT ATTACK)	;goal WONDER-ATTEMPT to ATTACK (constant)
=>
	(set-strategic-number sn-special-attack-type3 wonder);the AI considers attacking wonders
	(disable-self)				;only initialisation
)
