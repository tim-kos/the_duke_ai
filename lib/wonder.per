;========================================================================
;			BUILDING A WONDER
;========================================================================



#load-if-not-defined WONDER-RACE


;Rule1: INITIALISATION

(defrule
	(difficulty <= moderate)
=>
	(set-goal WONDER-ATTEMPT MAYBE)
	(disable-self)
)

(defrule
	(goal WONDER-ATTEMPT MAYBE)
	(taunt-detected any-ally 200)
=>
	(set-goal WONDER-ATTEMPT NO)
	(acknowledge-taunt this-any-ally 200)
)

(defrule
	(goal WONDER-ATTEMPT MAYBE)
	(current-age >= imperial-age)
	(military-population > TEN-PERCENT-POP)
	(wood-amount >= 600)
	(gold-amount >= 600)
	(stone-amount >= 600)
	(cc-players-building-type-count every-ally wonder == 0)
	(cc-players-building-type-count every-enemy wonder == 0)
	(cc-players-building-type-count every-neutral wonder == 0)
=>
	(set-goal WONDER-ATTEMPT POSSIBLE)
	(disable-self)
)

(defrule
	(goal WONDER-ATTEMPT POSSIBLE)
=>
	(disable-timer T-TRIBUTE)
	(enable-timer T-TRIBUTE 1)
	(disable-self)
)

(defrule
	(timer-triggered T-TRIBUTE)
	(goal WONDER-ATTEMPT POSSIBLE)
	(wood-amount < 1000)
=>
	(chat-to-allies "4 I need some wood to build a wonder!")
	(disable-timer T-TRIBUTE)
	(enable-timer T-TRIBUTE 120)
)

(defrule
	(timer-triggered T-TRIBUTE)
	(goal WONDER-ATTEMPT POSSIBLE)
	(gold-amount < 1000)
=>
	(chat-to-allies "5 I need some gold to build a wonder!")
	(disable-timer T-TRIBUTE)
	(enable-timer T-TRIBUTE 120)
)

(defrule
	(timer-triggered T-TRIBUTE)
	(goal WONDER-ATTEMPT POSSIBLE)
	(stone-amount < 1000)
=>
	(chat-to-allies "6 I need some stones to build a wonder!")
	(disable-timer T-TRIBUTE)
	(enable-timer T-TRIBUTE 120)
)

(defrule
	(goal WONDER-ATTEMPT POSSIBLE)
	(can-build-with-escrow wonder)
=>
	(set-strategic-number sn-maximum-town-size 12)
	(set-strategic-number sn-percent-civilian-builders 90)
	(set-strategic-number sn-cap-civilian-builders 50)
	(set-strategic-number sn-percent-civilian-gatherers 10)
;	(chat-local-to-self "It's time to build a wonder")
	(set-goal WONDER-ATTEMPT YES)
	(disable-timer T-TRIBUTE)
	(enable-timer T-TRIBUTE 30)
)

(defrule
	(goal WONDER-ATTEMPT YES)
	(can-build-with-escrow wonder)
=>
	(release-escrow wood)
	(release-escrow gold)
	(release-escrow stone)
	(build wonder)
	(set-goal WONDER-ATTEMPT MAYBE)
	(chat-to-player every-ally "39 Okay, I'll build a wonder, so please protect me!")
)

(defrule
	(goal WONDER-ATTEMPT YES)
	(not (can-build-with-escrow wonder))
=>
	(set-goal WONDER-ATTEMPT MAYBE)
	(chat-to-player every-ally "39 I can't build a wonder yet!")
)

(defrule
	(taunt-detected any-ally 37)
=>
 	(acknowledge-taunt this-any-ally 37)
	(chat-to-player this-any-ally "Okay, I'll build a wonder!")
	(set-goal WONDER-ATTEMPT POSSIBLE)
)

(defrule
	(building-type-count wonder > 0)
=>
	(set-strategic-number sn-percent-civilian-builders 15)
	(set-strategic-number sn-cap-civilian-builders 8)
	(set-strategic-number sn-percent-civilian-gatherers 85)
	(disable-self)
)

(defrule
	(building-type-count wonder == 0)
	(building-type-count-total wonder > 0)
	(cc-players-building-type-count any-ally wonder > 0)
	(or
		(enemy-captured-relics)
		(or
			(cc-players-building-type-count any-enemy wonder > 0)
			(cc-players-building-type-count any-neutral wonder > 0)
		)
	)
=>
	(delete-building wonder)
	(set-strategic-number sn-percent-civilian-builders 15)
	(set-strategic-number sn-cap-civilian-builders 8)
	(set-strategic-number sn-percent-civilian-gatherers 85)
	(set-goal WONDER-ATTEMPT MAYBE)
)



#end-if		;if not game type == wonder race
