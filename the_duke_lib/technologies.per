;************************************************************************************************************
;TECHNOLOGIES IN TOWN-CENTER

;LOOM - 50 GOLD
(defrule
	(or
		(and
			(not (can-train villager))
			(or
				(civilian-population > 6)
				(civ-selected chinese)
			)
		)
		(game-time > 420)
	)
 	(can-research ri-loom)
=>
 	(research ri-loom)
;	(chat-local-to-self "research LOOM")
)

;WHEEL-BARROW 175 FOOD, 50 WOOD
(defrule
	(can-research-with-escrow ri-wheel-barrow)
	(or
		(unit-type-count villager > 20)
		(and
			(food-amount > 300)
			(wood-amount > 300)
		)
	)
	(not (research-available ri-double-bit-axe))
=>
	(release-escrow food)
	(release-escrow wood)
 	(research ri-wheel-barrow)
;	(chat-local-to-self "research WHEEL-BARROW")
)

;HAND-CART - 300 FOOD, 200 WOOD
;Important: We need to do the important stuff with escrow resources (even though we don't use them),
;otherwise the game will take ages before it does the research, even though all conditions are met
(defrule
 	(can-research-with-escrow ri-hand-cart)
	(or
		(or
			(building-type-count-total town-center > 1)
			(town-under-attack)
		)
		(and
			(food-amount > 500)
			(wood-amount > 400)
		)
	)
	(or
		(unit-type-count villager > 30)
		(and
			(food-amount > 500)
			(wood-amount > 400)
		)
	)
	(not (research-available ri-bow-saw))
=>
	(release-escrow food)
	(release-escrow wood)
	(research ri-hand-cart)
;	(chat-local-to-self "research HAND-CART")
)

;200 WOOD, 300 FOOD
(defrule
 	(current-age == castle-age)
 	(can-research ri-hand-cart)
	(building-type-count-total lumber-camp >= 1)
	(or
		(unit-type-count-total eagle-warrior-line > 20)
		(unit-type-count-total knight-line > 10)
	)
	(goal RUSH-CONTROL RUSHING)
	(unit-type-count villager > 30)
=>
	(research ri-hand-cart)
; (chat-local-to-self "research HAND-CART")
)

;Rule95: research TOWN-WATCH
;75 FOOD
(defrule
	(food-amount > 600)
 	(can-research ri-town-watch)
	(building-type-count town-center > 1)
	(goal RUSH-CONTROL NO)
=>
 	(research ri-town-watch)
;	(chat-local-to-self "research TOWN-WATCH")
)

;300 FOOD, 200 GOLD
(defrule
	(food-amount > 600)
	(gold-amount > 500)
	(can-research ri-town-patrol)
	(not (goal RESEARCHING-IMPERIAL FAST-IMPERIAL))
	(building-type-count town-center > 2)
	(goal RUSH-CONTROL NO)
=>
 	(research ri-town-patrol)
;	(chat-local-to-self "research TOWN-PATROL")
)
;************************************************************************************************************
;TECHNOLOGIES IN LUMBER-CAMP


;DOUBLE-BIT-AXE 50 WOOD, 100 FOOD
(defrule
 	(can-research-with-escrow ri-double-bit-axe)
=>
	(release-escrow wood)
	(release-escrow food)
	(research ri-double-bit-axe)
; (chat-local-to-self "research DOUBLE-BIT-AXE")
)

;BOW-SAW 100 WOOD, 150 FOOD
(defrule
 	(can-research-with-escrow ri-bow-saw)
=>
	(release-escrow wood)
	(release-escrow food)
 	(research ri-bow-saw)
; (chat-local-to-self "research BOW-SAW")
)

;200 WOOD, 300 FOOD
(defrule
 	(can-research-with-escrow ri-two-man-saw)
=>
	(release-escrow wood)
	(release-escrow food)
 	(research ri-two-man-saw)
; (chat-local-to-self "research TWO-MAN-SAW")
)

;************************************************************************************************************
;************************************************************************************************************
;TECHNOLOGIES IN MILL


;HORSE-COLLAR 75 WOOD, 75 FOOD
(defrule
 	(can-research-with-escrow ri-horse-collar)
	(not (research-available ri-double-bit-axe))
=>
	(release-escrow wood)
	(release-escrow food)
 	(research ri-horse-collar)
; (chat-local-to-self "research HORSE-COLLAR")
)

;HEAVY-PLOW 125 WOOD, 125 FOOD
(defrule
 	(can-research-with-escrow ri-heavy-plow)
	(not (research-available ri-hand-cart))
	(not (research-available ri-bow-saw))
	(building-type-count-total town-center > 1)
	(or
		(goal RUSH-CONTROL NO)
		(and
			(or
				(unit-type-count-total eagle-warrior-line > 15)
				(unit-type-count-total knight-line > 12)
			)
			(goal RUSH-CONTROL RUSHING)
		)
	)
=>
	(release-escrow wood)
	(release-escrow food)
 	(research ri-heavy-plow)
; (chat-local-to-self "research HEAVY-PLOW")
)

;CROP ROTATION 250 WOOD, 250 FOOD
(defrule
 	(can-research-with-escrow ri-crop-rotation)
	(not (research-available ri-hand-cart))
	(not (research-available ri-two-man-saw))
=>
	(release-escrow wood)
	(release-escrow food)
 	(research ri-crop-rotation)
; (chat-local-to-self "research CROP-ROTATION")
)

;************************************************************************************************************
;************************************************************************************************************
;TECHNOLOGIES IN MINING-CAMP

;GOLD-MINING 75 WOOD, 100 FOOD
(defrule
 	(can-research-with-escrow ri-gold-mining)
	(strategic-number sn-gold-gatherer-percentage > 0)
	(or
		(not (goal RESEARCHING-CASTLE FAST-CASTLE))
 		(goal ADVANCING-TO-AGE CASTLE-AGE)
	)
	(not (research-available ri-wheel-barrow))
	(not (research-available ri-double-bit-axe))
	(not (research-available ri-horse-collar))
	(or
		(goal RUSH-CONTROL NO)
		(and
			(goal RUSH-CONTROL RUSHING)
			(or
				(unit-type-count-total eagle-warrior-line > FIVE-PERCENT-POP)
				(unit-type-count-total knight-line > THREE-PERCENT-POP)
			)
		)
	)
=>
	(release-escrow wood)
	(release-escrow food)
 	(research ri-gold-mining)
; (chat-local-to-self "research GOLD-MINING")
)

;GOLD-MINING 75 WOOD, 100 FOOD
(defrule
 	(can-research-with-escrow ri-gold-mining)
	(strategic-number sn-gold-gatherer-percentage > 0)
	(current-age >= castle-age)
	(not (research-available ri-bow-saw))
	(not (research-available ri-hand-cart))
	(building-type-count-total town-center > 1)
	(or
		(goal RUSH-CONTROL NO)
		(and
			(goal RUSH-CONTROL RUSHING)
			(or
				(unit-type-count-total eagle-warrior-line > FIVE-PERCENT-POP)
				(unit-type-count-total knight-line > THREE-PERCENT-POP)
			)
		)
	)
=>
	(release-escrow wood)
	(release-escrow food)
 	(research ri-gold-mining)
; (chat-local-to-self "research GOLD-MINING")
)

;GOLD-MINING 75 WOOD, 100 FOOD
(defrule
 	(can-research-with-escrow ri-gold-mining)
	(strategic-number sn-gold-gatherer-percentage > 0)
	(current-age == feudal-age)
	(goal ADVANCING-TO-AGE CASTLE-AGE)
	(goal PRIMARY-COMBAT-UNIT EAGLEMAN)
	(goal RUSH-CONTROL CASTLE)
=>
	(release-escrow wood)
	(release-escrow food)
 	(research ri-gold-mining)
; (chat-local-to-self "research GOLD-MINING")
)

;GOLD-SHAFT-MINING 150 WOOD, 200 FOOD
(defrule
 	(can-research-with-escrow ri-gold-shaft-mining)
	(strategic-number sn-gold-gatherer-percentage > 0)
	(not (research-available ri-hand-cart))
	(not (research-available ri-bow-saw))
	(not (research-available ri-heavy-plow))
	(food-amount > 400)
	(wood-amount > 400)
=>
	(release-escrow wood)
	(release-escrow food)
 	(research ri-gold-shaft-mining)
; (chat-local-to-self "research GOLD-SHAFT-MINING")
)

;STONE-MINING 125 WOOD, 125 FOOD
(defrule
 	(can-research-with-escrow ri-stone-mining)
	(strategic-number sn-stone-gatherer-percentage > 0)
	(not (research-available ri-gold-shaft-mining))
	(or
		(goal RUSH-CONTROL NO)
		(and
			(goal RUSH-CONTROL RUSHING)
			(or
				(unit-type-count-total eagle-warrior-line > FIVE-PERCENT-POP)
				(unit-type-count-total knight-line > THREE-PERCENT-POP)
			)
		)
	)
	(building-type-count-total town-center > 1)
	(wood-amount > 200)
	(food-amount > 200)
=>
	(release-escrow wood)
	(release-escrow food)
 	(research ri-stone-mining)
; (chat-local-to-self "research STONE-MINING")
)

;STONE-SHAFT-MINING 150 WOOD, 200 FOOD
(defrule
	(not (research-available ri-gold-shaft-mining))
 	(can-research-with-escrow ri-stone-shaft-mining)
	(strategic-number sn-stone-gatherer-percentage > 0)
	(food-amount > 500)
	(wood-amount > 500)
=>
	(release-escrow wood)
	(release-escrow food)
 	(research ri-stone-shaft-mining)
; (chat-local-to-self "research STONE-MINING")
)


#load-if-not-defined WONDER-RACE

;*******************************************************************
;*******************************************************************
;TECHNOLOGIES IN MARKET


;CARTOGRAPHY 100 FOOD, 100 GOLD
(defrule
 	(can-research-with-escrow ri-cartography)
	(player-in-game any-ally)
	(or
		(not (goal RESEARCHING-CASTLE FAST-CASTLE))
		(current-age >= castle-age)
	)
	(goal RUSH-CONTROL NO)
	(food-amount > 800)
	(gold-amount > 700)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-cartography)
; (chat-local-to-self "research CARTOGRAPHY")
)

;COINAGE 150 FOOD, 50 GOLD
(defrule
 	(can-research-with-escrow ri-coinage)
	(player-in-game any-ally)
	(current-age >= castle-age)
	(goal RUSH-CONTROL NO)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-coinage)
; (chat-local-to-self "research COINAGE")
)

;BANKING 200 FOOD, 100 GOLD
(defrule
 	(can-research-with-escrow ri-banking)
	(player-in-game any-ally)
	(or
		(not (goal RESEARCHING-IMPERIAL FAST-IMPERIAL))
		(current-age == imperial-age)
	)
	(goal RUSH-CONTROL NO)
	(food-amount > 1000)
	(gold-amount > 1000)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-banking)
; (chat-local-to-self "research BANKING")
)

;CARAVAN 200 FOOD, 200 GOLD
(defrule
 	(can-research-with-escrow ri-caravan)
	(player-in-game any-ally)
	(or
		(unit-type-count-total trade-cart > 5)
		(unit-type-count-total trade-cog > 5)
	)
	(food-amount > 1000)
	(gold-amount > 400)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-caravan)
; (chat-local-to-self "research CARAVAN")
)

;GUILDS 300 FOOD, 200 GOLD
(defrule
 	(can-research-with-escrow ri-guilds)
	(food-amount > 1000)
	(gold-amount > 800)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-guilds)
; (chat-local-to-self "research GUILDS")
)


;************************************************************************************************************
;************************************************************************************************************
;TECHNOLOGIES IN UNIVERSITY


;MASONRY 175 WOOD, 150 FOOD
(defrule
 	(can-research ri-masonry)
	(building-type-count-total university >= 1)
	(building-type-count-total castle >= 2)
	(not (research-available ri-ballistics))
	(building-type-count town-center > 2)
	(food-amount > 800)
	(wood-amount > 600)
=>
 	(research ri-masonry)
; (chat-local-to-self "research MASONRY")
)

;GUARD-TOWER 125 WOOD, 125 STONE
(defrule
 	(can-research ri-guard-tower)
	(building-type-count-total watch-tower-line > 1)
	(not (research-available ri-ballistics))
	(building-type-count town-center > 2)
	(food-amount > 800)
	(wood-amount > 600)
	(stone-amount > 300)
=>
	(research ri-guard-tower)
; (chat-local-to-self "research GUARD-TOWER")
)

;MURDER-HOLES 200 FOOD, 200 STONE
(defrule
 	(can-research ri-murder-holes)
	(building-type-count town-center > 2)
	(food-amount > 600)
	(wood-amount > 400)
=>
 	(research ri-murder-holes)
; (chat-local-to-self "research MURDER-HOLES")
)

;BALLISTICS 300 WOOD, 175 GOLD
(defrule
 	(can-research ri-ballistics)
	(or
		(goal ABLE-TO-UPGRADE-ARCHERS HIGH)
		(building-type-count-total castle >= 1)
	)
	(not (research-available ri-murder-holes))
	(building-type-count town-center > 2)
	(food-amount > 600)
	(wood-amount > 400)
=>
	(research ri-ballistics)
; (chat-local-to-self "research BALLISTICS")
)

;FORTIFIED WALL 200 FOOD, 100 WOOD
(defrule
 	(can-research ri-fortified-wall)
	(wall-completed-percentage 2 > 60)
	(not (research-available ri-ballistics))
	(not (research-available ri-murder-holes))
	(food-amount > 600)
	(wood-amount > 400)
=>
	(research ri-fortified-wall)
; (chat-local-to-self "research FORTIFIED-WALL")
)

;HEATED-SHOT 350 FOOD, 100 GOLD
(defrule
 	(can-research ri-heated-shot)
	(not (goal MAP-KIND LAND-MAP))
	(not (research-available ri-ballistics))
	(not (research-available ri-murder-holes))
	(food-amount > 600)
	(wood-amount > 400)
=>
	(research ri-heated-shot)
; (chat-local-to-self "research HEATED-SHOT")
)


;***********************************************
;***********************************************
#else		;if not game type == wonder race
;***********************************************
;***********************************************


;STONECUTTING 200 WOOD, 300 FOOD
(defrule
 	(can-research ri-stonecutting)
=>
	(research ri-stonecutting)
; (chat-local-to-self "research STONECUTTING")
)

;***********************************************
;***********************************************
#end-if		;if  game type == wonder race



#load-if-not-defined WONDER-RACE
;***********************************************
;***********************************************


;Rule207: research STONECUTTING- MODERATE, HARD, HARDEST
;200 WOOD, 300 FOOD
(defrule
 	(current-age >= castle-age)
 	(can-research ri-stonecutting)
	(building-type-count-total university >= 1)
	(not (research-available ri-ballistics))
=>
	(research ri-stonecutting)
; (chat-local-to-self "research STONECUTTING")
)
;500 FOOD, 600 WOOD
(defrule
 	(can-research ri-siege-engineers)
	(or
		(or
			(unit-type-count-total battering-ram-line > 1)
			(unit-type-count-total mangonel-line > 1)
		)
		(unit-type-count-total scorpion-line > 3)
	)
	(food-amount > 1000)
	(wood-amount > 700)
	(not (research-available ri-ballistics))
	(building-type-count town-center > 1)
=>
 	(research ri-siege-engineers)
; (chat-local-to-self "research SIEGE-ENGINEERS")
)
;500 FOOD, 600 WOOD
(defrule
 	(can-research ri-siege-engineers)
	(or
		(unit-type-count-total bombard-cannon > 1)
		(unit-type-count-total trebuchet > 1)
	)
	(food-amount > 1000)
	(wood-amount > 700)
	(not (research-available ri-ballistics))
	(building-type-count town-center > 1)
=>
 	(research ri-siege-engineers)
; (chat-local-to-self "research SIEGE-ENGINEERS")
)
;200 WOOD, 300 FOOD
(defrule
 	(current-age >= imperial-age)
 	(can-research ri-architecture)
	(building-type-count-total university >= 1)
	(not (research-available ri-ballistics))
	(building-type-count town-center > 1)
=>
 	(research ri-architecture)
; (chat-local-to-self "research ARCHITECTURE")
)
;500 WOOD, 350 STONE
(defrule
 	(current-age >= imperial-age)
 	(can-research ri-keep)
	(building-type-count-total university >= 1)
	(building-type-count-total watch-tower-line > 3)
	(building-type-count-total castle >= 2)
	(not (research-available ri-ballistics))
	(building-type-count town-center > 1)
=>
 	(research ri-keep)
; (chat-local-to-self "research KEEP")
)
;300 FOOD, 200 GOLD
(defrule
 	(current-age >= imperial-age)
 	(can-research ri-chemistry)
	(building-type-count-total university >= 1)
	(not (research-available ri-ballistics))
=>
 	(research ri-chemistry)
; (chat-local-to-self "research CHEMISTRY")
)
;800 FOOD, 400 WOOD
(defrule
 	(current-age >= imperial-age)
 	(can-research ri-bombard-tower)
	(building-type-count-total university >= 1)
	(not (research-available ri-ballistics))
	(building-type-count town-center > 1)
=>
	(research ri-bombard-tower)
; (chat-local-to-self "research BOMBARD-TOWER")
)


;************************************************************************************************************
;TECHNOLOGIES IN MONASTERY



;Rule214: research ATONEMENT
;325 GOLD
(defrule
 	(can-research-with-escrow ri-atonement)
	(cc-players-unit-type-count any-enemy monk >= 4)
	(gold-amount > 2000)
=>
	(release-escrow gold)
 	(research ri-atonement)
;	(chat-local-to-self "research ATONEMENT")
)
;140 GOLD
(defrule
 	(can-research-with-escrow ri-fervor)
	(unit-type-count monk >= 3)
	(gold-amount > 1700)
=>
	(release-escrow gold)
	(research ri-fervor)
;	(chat-local-to-self "research FERVOR")
)
;120 GOLD
(defrule
 	(can-research-with-escrow ri-sanctity)
	(unit-type-count monk >= 3)
	(gold-amount > 1700)
=>
	(release-escrow gold)
 	(research ri-sanctity)
;	(chat-local-to-self "research SANCTITY")
)
;475 GOLD
(defrule
 	(can-research-with-escrow ri-redemption)
	(unit-type-count monk >= 3)
	(gold-amount > 2500)
	(or
		(not (death-match-game))
		(game-time > 1800)
	)
=>
	(release-escrow gold)
 	(research ri-redemption)
;	(chat-local-to-self "research REDEMPTION")
)
;350 GOLD
;(defrule
; (can-research-with-escrow ri-herbal-medicine)
;	(gold-amount > 2000)
;=>
;	(release-escrow gold)
;	(research ri-herbal-medicine)
;	(chat-local-to-self "research HERBAL-MEDICINE")
;)

;HERESY 1000 GOLD
(defrule
 	(can-research-with-escrow ri-heresy)
	(gold-amount > 4000)
	(military-population > 30)
	(or
		(not (death-match-game))
		(game-time > 1800)
	)
=>
	(release-escrow gold)
 	(research ri-heresy)
;	(chat-local-to-self "research HERESY")
)
;750 FOOD, 1000 GOLD
(defrule
 	(can-research-with-escrow ri-faith)
	(gold-amount > 2500)
	(military-population > 30)
	(food-amount > 2500)
	(or
		(not (death-match-game))
		(game-time > 1800)
	)
=>
	(release-escrow gold)
 	(research ri-faith)
;	(chat-local-to-self "research FAITH")
)
;120 GOLD
(defrule
 	(can-research-with-escrow ri-illumination)
	(unit-type-count monk >= 3)
	(gold-amount > 1700)
=>
	(release-escrow gold)
	(research ri-illumination)
;	(chat-local-to-self "research ILLUMINATION")
)
;200 GOLD
(defrule
	(can-research ri-block-printing)
	(building-type-count-total monastery >= 1)
	(unit-type-count monk >= 3)
	(gold-amount > 1700)
=>
	(release-escrow gold)
	(research ri-block-printing)
;	(chat-local-to-self "research BLOCK-PRINTING")
)
;400 FOOD, 800 GOLD
(defrule
	(can-research ri-theocracy)
	(unit-type-count monk >= 4)
	(gold-amount > 3000)
	(or
		(not (death-match-game))
		(game-time > 1800)
	)
=>
	(release-escrow gold)
	(research ri-theocracy)
;	(chat-local-to-self "research THEOCRACY")
)


;************************************************************************************************************
;************************************************************************************************************
;TECHNOLOGIES IN BARRACKS


;TRACKING 75 FOOD
(defrule
 	(can-research-with-escrow ri-tracking)
	(or
		(goal ABLE-TO-UPGRADE-INFANTRY MIDDLE)
		(goal ABLE-TO-UPGRADE-INFANTRY HIGH)
	)
	(food-amount > 500)
=>
	(release-escrow food)
 	(research ri-tracking)
; (chat-local-to-self "research TRACKING")
)
;200 FOOD
(defrule
 	(can-research-with-escrow ri-squires)
	(or
		(goal ABLE-TO-UPGRADE-INFANTRY MIDDLE)
		(goal ABLE-TO-UPGRADE-INFANTRY HIGH)
	)
	(food-amount > 500)
=>
	(release-escrow food)
 	(research ri-squires)
; (chat-local-to-self "research SQUIRES")
)

;Rule228: research MAN-AT-ARMS- EASY, MODERATE, HARD, HARDEST, part 1
;100 FOOD, 40 GOLD
(defrule
 	(can-research-with-escrow ri-man-at-arms)
	(or
		(unit-type-count-total militiaman-line > 1)
		(and
			(and
				(goal SECONDARY-COMBAT-UNIT INFANTRY1)
				(goal RUSH-CONTROL CASTLE)
			)
			(goal ADVANCING-TO-AGE CASTLE-AGE)
		)
	)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-man-at-arms)
; (chat-local-to-self "research MAN-AT-ARMS")
)

;LONG-SWORDSMAN 200 FOOD, 65 GOLD
(defrule
 	(can-research-with-escrow ri-long-swordsman)
	(or
		(unit-type-count-total militiaman-line > TWO-PERCENT-POP)
		(and
			(goal RUSH-CONTROL RUSHING)
			(goal SECONDARY-COMBAT-UNIT INFANTRY1)
		)
	)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-long-swordsman)
; (chat-local-to-self "research LONG-SWORDSMAN")
)

;300 FOOD, 100 GOLD
(defrule
 	(can-research-with-escrow ri-two-handed-swordsman)
	(or
		(unit-type-count-total militiaman-line > THREE-PERCENT-POP)
		(or
			(goal PRIMARY-COMBAT-UNIT INFANTRY)
			(goal PRIMARY-COMBAT-UNIT INFANTRY1)
		)
	)
=>
	(release-escrow food)
	(release-escrow gold)
	(research ri-two-handed-swordsman)
; (chat-local-to-self "research TWO-HANDED-SWORDSMAN")
)


;CHAMPION
;750 FOOD, 350 GOLD
(defrule
 	(can-research-with-escrow ri-champion)
	(unit-type-count-total militiaman-line > 20)
	(food-amount > 1500)
	(gold-amount > 600)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-champion)
; (chat-local-to-self "research CHAMPION")
)



;PIKEMAN 215 FOOD, 90 GOLD
(defrule
 	(can-research-with-escrow ri-pikeman)
	(or
		(unit-type-count-total spearman-line >= ONE-PERCENT-POP)
		(and
			(goal RUSH-CONTROL RUSHING)
			(goal SECONDARY-COMBAT-UNIT SPEARMAN)
		)
	)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-pikeman)
; (chat-local-to-self "research PIKEMAN")
)

;500 FOOD, 600 GOLD
(defrule
 	(can-research-with-escrow ri-halberdier)
	(or
		(unit-type-count-total spearman-line >= THREE-PERCENT-POP)
		(or
			(goal PRIMARY-COMBAT-UNIT SPEARMAN)
			(goal SECONDARY-COMBAT-UNIT SPEARMAN)
		)
	)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-halberdier)
; (chat-local-to-self "research HALBERDIER")
)

;215 FOOD, 90 GOLD
(defrule
 	(can-research-with-escrow ri-elite-eagle-warrior)
	(or
		(unit-type-count-total eagle-warrior-line >= THREE-PERCENT-POP)
		(or
			(goal PRIMARY-COMBAT-UNIT EAGLEMAN)
			(goal SECONDARY-COMBAT-UNIT EAGLEMAN)
		)
	)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-elite-eagle-warrior)
; (chat-local-to-self "research ELITE-EAGLE-WARRIOR")
)
;************************************************************************************************************
;TECHNOLOGIES IN ARCHERY-RANGE



;CROSSBOW 125 FOOD, 75 GOLD
(defrule
 	(can-research-with-escrow ri-crossbow)
	(unit-type-count-total archer-line > 5)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-crossbow)
; (chat-local-to-self "research CROSSBOW")
)

;350 FOOD, 300 GOLD
(defrule
 	(can-research-with-escrow ri-arbalest)
	(unit-type-count-total archer-line >= 15)
	(food-amount > 500)
	(gold-amount > 400)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-arbalest)
; (chat-local-to-self "research ARBALEST")
)

;250 WOOD, 160 GOLD
(defrule
 	(can-research-with-escrow ri-elite-skirmisher)
 	(unit-type-count-total skirmisher-line >= 9)
 	(or
		(wood-amount > 350)
		(building-type-count-total town-center > 2)
	)
	(building-type-count-total town-center > 1)
=>
	(release-escrow wood)
	(release-escrow gold)
	(research ri-elite-skirmisher)
; (chat-local-to-self "research ELITE-SKIRMISHER")
)

;900 FOOD, 500 GOLD
(defrule
 	(can-research-with-escrow ri-heavy-cavalry-archer)
	(unit-type-count-total cavalry-archer-line >= 15)
	(food-amount > 1500)
	(gold-amount > 900)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-heavy-cavalry-archer)
; (chat-local-to-self "research HEAVY-CAVALRY-ARCHER")
)
;350 FOOD, 250 GOLD
(defrule
 	(can-research-with-escrow ri-thumb-ring)
	(or
		(goal ABLE-TO-UPGRADE-ARCHERS MIDDLE)
		(goal ABLE-TO-UPGRADE-ARCHERS HIGH)
	)
	(food-amount > 600)
	(gold-amount > 600)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-thumb-ring)
; (chat-local-to-self "research THUMB-RING")
)

;200 FOOD, 250 GOLD
(defrule
 	(can-research-with-escrow ri-parthian-tactics)
	(or
		(goal ABLE-TO-UPGRADE-ARCHERS MIDDLE)
		(goal ABLE-TO-UPGRADE-ARCHERS HIGH)
	)
	(food-amount > 400)
	(gold-amount > 400)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-parthian-tactics)
; (chat-local-to-self "research PARTHIAN-TACTICS")
)
;************************************************************************************************************
;TECHNOLOGIES IN STABLE



;HUSBANDRY 250 FOOD
(defrule
 	(can-research-with-escrow ri-husbandry)
	(or
		(goal ABLE-TO-UPGRADE-CAVALRY MIDDLE)
		(goal ABLE-TO-UPGRADE-CAVALRY HIGH)
	)
	(not (research-available ri-bloodlines))
=>
	(release-escrow food)
 	(research ri-husbandry)
; (chat-local-to-self "research HUSBANDRY")
)

;BLOODLINES 150 FOOD, 100 GOLD
(defrule
 	(can-research-with-escrow ri-bloodlines)
	(or
		(goal ABLE-TO-UPGRADE-CAVALRY MIDDLE)
		(goal ABLE-TO-UPGRADE-CAVALRY HIGH)
	)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-bloodlines)
; (chat-local-to-self "research BLOODLINES")
)

;LIGHT CAVALRY 150 FOOD, 50 GOLD
(defrule
 	(can-research-with-escrow ri-light-cavalry)
	(unit-type-count-total scout-cavalry-line > 1)
	(food-amount > 300)
	(gold-amount > 150)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-light-cavalry)
; (chat-local-to-self "research LIGHT-CAVALRY")
)

;HUSSAR 500 FOOD, 600 GOLD
(defrule
 	(can-research-with-escrow ri-hussar)
	(or
		(unit-type-count-total scout-cavalry-line >= 10)
		(or
			(goal PRIMARY-COMBAT-UNIT LCAVALRY)
			(goal SECONDARY-COMBAT-UNIT LCAVALRY)
		)
	)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-hussar)
; (chat-local-to-self "research HUSSAR")
)

;CAVALIER 300 FOOD, 300 GOLD
(defrule
 	(can-research-with-escrow ri-cavalier)
 	(unit-type-count-total knight-line >= TWO-PERCENT-POP)
	(or
		(goal PRIMARY-COMBAT-UNIT KNIGHT)
		(or
			(goal SECONDARY-COMBAT-UNIT KNIGHT)
			(unit-type-count-total knight-line >= 10)
		)
	)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-cavalier)
; (chat-local-to-self "research CAVALIER")
)


;PALADIN 1300 FOOD, 750 GOLD
(defrule
 	(can-research-with-escrow ri-paladin)
	(unit-type-count-total knight-line >= THREE-PERCENT-POP)
	(or
		(goal PRIMARY-COMBAT-UNIT KNIGHT)
		(or
			(goal SECONDARY-COMBAT-UNIT KNIGHT)
			(unit-type-count-total knight-line >= 20)
		)
	)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-paladin)
; (chat-local-to-self "research PALADIN")
)

;1300 FOOD, 750 GOLD
(defrule
 	(can-research-with-escrow ri-heavy-camel)
	(unit-type-count-total camel-line >= THREE-PERCENT-POP)
	(or
		(goal PRIMARY-COMBAT-UNIT CAMEL)
		(or
			(goal SECONDARY-COMBAT-UNIT CAMEL)
			(unit-type-count-total camel-line >= 20)
		)
	)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-heavy-camel)
; (chat-local-to-self "research HEAVY-CAMEL")
)



;************************************************************************************************************
;************************************************************************************************************
;TECHNOLOGIES IN SIEGE-WORKSHOP



;CAPPED-RAM 300 FOOD
(defrule
 	(can-research-with-escrow ri-capped-ram)
	(unit-type-count-total battering-ram-line > 1)
	(food-amount > 700)
=>
	(release-escrow food)
	(research ri-capped-ram)
; (chat-local-to-self "research CAPPED-RAM")
)


;SIEGE-RAM 1000 FOOD
(defrule
 	(can-research-with-escrow ri-siege-ram)
	(or
		(goal PRIMARY-COMBAT-SIEGE-UNIT RAM)
		(goal SECONDARY-COMBAT-SIEGE-UNIT RAM)
	)
	(food-amount > 1800)
	(unit-type-count-total battering-ram-line > 2)
=>
	(release-escrow food)
 	(research ri-siege-ram)
; (chat-local-to-self "research SIEGE-RAM")
)


;ONAGER 800 FOOD, 500 GOLD
(defrule
 	(can-research-with-escrow ri-onager)
	(or
		(goal PRIMARY-COMBAT-SIEGE-UNIT MANGONEL)
		(goal SECONDARY-COMBAT-SIEGE-UNIT MANGONEL)
	)
	(unit-type-count-total mangonel-line > 2)
	(food-amount > 1000)
	(gold-amount > 700)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-onager)
; (chat-local-to-self "research ONAGER")
)


;SIEGE-ONAGER 1450 FOOD, 1000 GOLD
(defrule
 	(can-research-with-escrow ri-siege-onager)
 	(unit-type-count-total mangonel-line > 3)
	(or
		(goal PRIMARY-COMBAT-SIEGE-UNIT MANGONEL)
		(goal SECONDARY-COMBAT-SIEGE-UNIT MANGONEL)
	)
	(food-amount > 2600)
	(gold-amount > 1500)
=>
	(release-escrow food)
	(release-escrow gold)
	(research ri-siege-onager)
; (chat-local-to-self "research SIEGE-ONAGER")
)

;HEAVY-SCORPION 1000 FOOD, 1100 WOOD
(defrule
 	(can-research-with-escrow ri-heavy-scorpion)
	(or
		(goal PRIMARY-COMBAT-SIEGE-UNIT SCORPION)
		(goal SECONDARY-COMBAT-SIEGE-UNIT SCORPION)
	)
	(unit-type-count-total scorpion-line > 3)
	(wood-amount > 1400)
	(food-amount > 1500)
=>
	(release-escrow food)
	(release-escrow gold)
	(research ri-heavy-scorpion)
; (chat-local-to-self "research HEAVY-SCORPION")
)
;************************************************************************************************************
;TECHNOLOGIES IN DOCK


;HEAVY-DEMOLITION-SHIP 200 WOOD, 300 GOLD
(defrule
	(not (goal MAP-KIND LAND-MAP))
	(can-research ri-heavy-demolition-ship)
	(unit-type-count-total demolition-ship-line > 1)
	(wood-amount > 800)
	(gold-amount > 800)
=>
	(research ri-heavy-demolition-ship)
;	(chat-local-to-self "research HEAVY-DEMOLITION-SHIP")
)


;DRY-DOCK 600 FOOD, 400 GOLD
(defrule
	(can-research ri-dry-dock)
	(not (goal MAP-KIND LAND-MAP))
	(not (goal RUSH-CONTROL RUSHING))
	(food-amount > 800)
	(gold-amount > 600)
=>
	(research ri-dry-dock)
;	(chat-local-to-self "research DRY-DOCK")
)
;1000 FOOD, 300 GOLD
(defrule
	(warboat-count > FIVE-PERCENT-POP)
	(can-research ri-shipwright)
	(not (goal RUSH-CONTROL RUSHING))
	(not (goal MAP-KIND LAND-MAP))
	(food-amount > 1200)
	(gold-amount > 500)
=>
	(research ri-shipwright)
;	(chat-local-to-self "research SHIPWRIGHT")
)
;250 FOOD, 150 GOLD
(defrule
	(can-research ri-careening)
	(not (goal RUSH-CONTROL RUSHING))
	(not (goal MAP-KIND LAND-MAP))
=>
	(research ri-careening)
;	(chat-local-to-self "research CAREENING")
)
;280 WOOD, 250 GOLD
(defrule
	(unit-type-count-total fire-ship-line > THREE-PERCENT-POP)
	(can-research ri-fast-fire-ship)
	(not (goal MAP-KIND LAND-MAP))
	(or
		(building-type-count-total town-center > 1)
		(wood-amount > 575)
	)
=>
	(research ri-fast-fire-ship)
;	(chat-local-to-self "research FAST-FIRE-SHIP")
)
;500 WOOD, 400 FOOD
(defrule
	(can-research ri-cannon-galleon)
	(not (goal MAP-KIND LAND-MAP))
	(or
		(building-type-count-total town-center > 1)
		(wood-amount > 775)
	)
=>
	(research ri-cannon-galleon)
;	(chat-local-to-self "research CANNON-GALLEON")
)


;Rule276: research DECK-GUNS - HARD, HARDEST
;525 WOOD, 500 GOLD
(defrule
	(unit-type-count-total cannon-galleon > 0)
	(can-research ri-deck-guns)
	(or
		(building-type-count-total town-center > 1)
		(wood-amount > 800)
	)
=>
	(research ri-deck-guns)
;	(chat-local-to-self "research DECK-GUNS")
)


;Rule277: research WAR-GALLEY - MODERATE, HARD, HARDEST
;230 FOOD, 100 GOLD
(defrule
	(unit-type-count-total galley-line > 1)
	(can-research ri-war-galley)
	(nand
		(civ-selected viking)
		(building-type-count-total castle > 0)
	)
	(not (goal MAP-KIND LAND-MAP))
=>
	(research ri-war-galley)
;	(chat-local-to-self "research WAR-GALLEY")
)
;315 WOOD, 400 FOOD
(defrule
	(can-research ri-galleon)
	(nand
		(civ-selected viking)
		(building-type-count-total castle > 0)
	)
	(not (goal MAP-KIND LAND-MAP))
	(or
		(building-type-count-total town-center > 1)
		(wood-amount > 590)
	)
=>
	(research ri-galleon)
;	(chat-local-to-self "research GALLEON")
)

;ELITE-LONGBOAT 750 FOOD, 475 GOLD
(defrule
	(can-research ri-elite-longboat)
	(unit-type-count-total longboat-line >= THREE-PERCENT-POP)
	(not (goal MAP-KIND LAND-MAP))
=>
	(research ri-elite-longboat)
;	(chat-local-to-self "research ELITE-LONGBOAT")
)

;1000 FOOD, 800 GOLD
(defrule
	(can-research ri-elite-turtle-ship)
	(unit-type-count-total turtle-ship-line >= THREE-PERCENT-POP)
	(not (goal MAP-KIND LAND-MAP))
	(food-amount > 1200)
	(gold-amount > 1000)
=>
	(research ri-elite-turtle-ship)
;	(chat-local-to-self "research ELITE-TURTLE-SHIP")
)


;************************************************************************************************************
;************************************************************************************************************
;TECHNOLOGIES IN CASTLE



;Rule282: research HOARDINGS-  MODERATE, HARD, HARDEST
;400 WOOD, 400 STONE
(defrule
 	(can-research ri-hoardings)
	(building-type-count-total castle >= 3)
=>
 	(research ri-hoardings)
; (chat-local-to-self "research HOARDINGS")
)

;150 FOOD, 150 GOLD
(defrule
 	(can-research ri-conscription)
	(food-amount > 300)
	(gold-amount > 300)
=>
 	(research ri-conscription)
; (chat-local-to-self "research CONSCRIPTION")
)
;400 FOOD, 200 GOLD
;note this technology only gives stronger villagers to us and I didn't write any strategy that uses this technology. That's why only research if
;we've got tons of goods
(defrule
 	(can-research ri-sappers)
	(building-type-count-total castle >= 1)
	(food-amount > 2000)
	(gold-amount > 1500)
=>
 	(research ri-sappers)
; (chat-local-to-self "research SAPPERS")
)

#end-if ;if not game type wonder-race
