;reset the ADVANCING-TO-AGE - goal
(defrule
	(current-age == dark-age)
=>
	(set-goal ADVANCING-TO-AGE NO)
	(disable-self)
)

(defrule
	(current-age == feudal-age)
=>
	(set-goal ADVANCING-TO-AGE NO)
	(disable-self)
)

(defrule
	(current-age == castle-age)
=>
	(set-goal ADVANCING-TO-AGE NO)
	(disable-self)
)

(defrule
	(current-age == imperial-age)
=>
	(set-goal ADVANCING-TO-AGE NO)
	(disable-self)
)

#load-if-not-defined WONDER-RACE
;***********************************************
;***********************************************



;************************************************************************************************************
;************************************************************************************************************
;AGE ADVANCEMENT


;Rule1: generate a random number if difficulty == hard or hardest that will define how long it'll take to advance to those several ages

(defrule
	(game-time > 14)
=>
	(generate-random-number 9)
	(disable-self)
)
;************************************************
;FEUDAL-AGE


;Rule5: define the "Middle-Feudal" -Strategy

(defrule
	(game-time > 14)
	(current-age == dark-age)
	(or
		(and
			(random-number >= 5)
			(goal RUSH-CONTROL NO)
		)
		(goal RUSH-CONTROL FEUDAL)
	)
=>
	(set-goal RESEARCHING-FEUDAL MIDDLE-FEUDAL)
	(chat-local-to-self "MIDDLE-FEUDAL")
	(disable-self)
)

(defrule
	(game-time > 14)
	(current-age == dark-age)
	(goal RUSH-CONTROL CASTLE)
=>
	(set-goal RESEARCHING-FEUDAL LATE-FEUDAL)
	(chat-local-to-self "LATE-FEUDAL")
	(disable-self)
)

(defrule
	(game-time > 14)
	(goal RUSH-CONTROL NO)
	(random-number < 5)
	(current-age == dark-age)
=>
	(set-goal RESEARCHING-FEUDAL LATE-FEUDAL)
	(chat-local-to-self "LATE-FEUDAL")
	(disable-self)
)
;************************************************
;CASTLE-AGE



;Rule11: define the "Fast-Castle" -Strategy (attacker)

(defrule
	(game-time > 14)
	(or
		(or
			(goal RUSH-CONTROL CASTLE)
			(goal RESEARCHING-FEUDAL LATE-FEUDAL)
		)
		(and
			(goal RESEARCHING-FEUDAL MIDDLE-FEUDAL)
			(not (goal RUSH-CONTROL FEUDAL))
		)
	)
	(current-age <= feudal-age)
=>
	(set-goal RESEARCHING-CASTLE FAST-CASTLE)
	(chat-local-to-self "FAST-CASTLE")
	(disable-self)
)

(defrule
	(game-time > 14)
	(goal RUSH-CONTROL NO)
	(current-age <= feudal-age)
	(random-number < 6)
=>
	(set-goal RESEARCHING-CASTLE FAST-CASTLE)
	(chat-local-to-self "FAST-CASTLE")
	(disable-self)
)

(defrule
	(game-time > 14)
	(goal RUSH-CONTROL NO)
	(random-number >= 6)
	(current-age <= feudal-age)
=>
	(set-goal RESEARCHING-CASTLE MIDDLE-CASTLE)
	(chat-local-to-self "MIDDLE-CASTLE")
	(disable-self)
)

(defrule
	(game-time > 14)
	(goal RUSH-CONTROL FEUDAL)
	(current-age <= feudal-age)
=>
	(set-goal RESEARCHING-CASTLE LATE-CASTLE)
	(chat-local-to-self "LATE-CASTLE")
	(disable-self)
)
;************************************************
;IMPERIAL-AGE



;Rule23: define the "Middle-Imperial" -Strategy (attacker)

(defrule
	(game-time > 14)
	(random-number <= 4)
	(or
		(goal RUSH-CONTROL NO	)
		(goal RUSH-CONTROL FEUDAL)
	)
	(current-age <= castle-age)
=>
	(set-goal RESEARCHING-IMPERIAL MIDDLE-IMPERIAL)
	(chat-local-to-self "MIDDLE-IMPERIAL")
	(disable-self)
)

(defrule
	(game-time > 14)
	(or
		(and
			(and
				(random-number > 4)
				(random-number <= 9)
			)
			(or
				(goal RUSH-CONTROL NO)
				(goal RUSH-CONTROL FEUDAL)
			)
		)
		(goal RUSH-CONTROL CASTLE)
	)
	(current-age <= castle-age)
=>
	(set-goal RESEARCHING-IMPERIAL LATE-IMPERIAL)
	(chat-local-to-self "LATE-IMPERIAL")
	(disable-self)
)

;Rule103: research FEUDAL-AGE3-MODERATE, HARD, HARDEST -fast feudal-age
;500 FOOD
(defrule
 	(current-age == dark-age)
 	(game-time > FEUDAL-RESEARCH-TIME-FAST-FEUDAL)
 	(can-research-with-escrow feudal-age)
	(goal RESEARCHING-FEUDAL FAST-FEUDAL)
=>
	(release-escrow food)
	(research feudal-age)
	(chat-to-player every-ally "39 I'm advancing to the feudal-age now!")
	(chat-local-to-self "advance to FEUDAL-AGE")
	(set-goal ADVANCING-TO-AGE FEUDAL-AGE)
)
;500 FOOD
(defrule
 	(current-age == dark-age)
 	(game-time > FEUDAL-RESEARCH-TIME-MIDDLE-FEUDAL)
	(can-research-with-escrow feudal-age)
	(goal RESEARCHING-FEUDAL MIDDLE-FEUDAL)
=>
	(release-escrow food)
	(research feudal-age)
	(chat-to-player every-ally "39 I'm advancing to the feudal-age now!")
	(chat-local-to-self "advance to FEUDAL-AGE")
	(set-goal ADVANCING-TO-AGE FEUDAL-AGE)
)
;500 FOOD
(defrule
 	(current-age == dark-age)
 	(game-time > FEUDAL-RESEARCH-TIME-LATE-FEUDAL)
	(can-research-with-escrow feudal-age)
	(goal RESEARCHING-FEUDAL LATE-FEUDAL)
=>
	(release-escrow food)
	(research feudal-age)
	(chat-to-player every-ally "39 I'm advancing to the feudal-age now!")
	(chat-local-to-self "advance to FEUDAL-AGE")
	(set-goal ADVANCING-TO-AGE FEUDAL-AGE)
)

;Rule109: research CASTLE-AGE4- MODERATE, HARD, HARDEST -fast castle-age
;800 FOOD, 200 GOLD
(defrule
 	(current-age == feudal-age)
	(game-time > CASTLE-RESEARCH-TIME-FAST-CASTLE)
 	(can-research-with-escrow castle-age)
 	(goal RESEARCHING-CASTLE FAST-CASTLE)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research castle-age)
	(chat-to-player every-ally "39 I'm advancing to the castle-age now!")
	(chat-local-to-self "advance to CASTLE-AGE")
	(set-goal ADVANCING-TO-AGE CASTLE-AGE)
	(enable-timer T-ABLE-TO-PAY-RESOURCES 15)
)
;800 FOOD, 200 GOLD
(defrule
 	(current-age == feudal-age)
	(game-time > CASTLE-RESEARCH-TIME-MIDDLE-CASTLE)
 	(can-research-with-escrow castle-age)
 	(goal RESEARCHING-CASTLE MIDDLE-CASTLE)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research castle-age)
	(chat-to-player every-ally "39 I'm advancing to the castle-age now!")
	(chat-local-to-self "advance to CASTLE-AGE")
	(set-goal ADVANCING-TO-AGE CASTLE-AGE)
	(enable-timer T-ABLE-TO-PAY-RESOURCES 15)
)
;800 FOOD, 200 GOLD
(defrule
 	(current-age == feudal-age)
	(game-time > CASTLE-RESEARCH-TIME-LATE-CASTLE)
 	(can-research-with-escrow castle-age)
 	(goal RESEARCHING-CASTLE LATE-CASTLE)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research castle-age)
	(chat-to-player every-ally "39 I'm advancing to the castle-age now!")
	(chat-local-to-self "advance to CASTLE-AGE")
	(set-goal ADVANCING-TO-AGE CASTLE-AGE)
	(enable-timer T-ABLE-TO-PAY-RESOURCES 15)
)

;Rule115: research IMPERIAL-AGE4- HARD, HARDEST -fast imperial-age
;1000 FOOD, 800 GOLD
(defrule
 	(current-age == castle-age)
	(game-time > IMPERIAL-RESEARCH-TIME-FAST-IMPERIAL)
 	(can-research-with-escrow imperial-age)
 	(goal RESEARCHING-IMPERIAL FAST-IMPERIAL)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research imperial-age)
	(chat-to-player every-ally "39 I'm advancing to the imperial-age now!")
	(chat-local-to-self "advance to IMPERIAL-AGE")
	(set-goal ADVANCING-TO-AGE IMPERIAL-AGE)
	(enable-timer T-ABLE-TO-PAY-RESOURCES 15)
)
;1000 FOOD, 800 GOLD
(defrule
 	(current-age == castle-age)
	(game-time > IMPERIAL-RESEARCH-TIME-MIDDLE-IMPERIAL)
 	(can-research-with-escrow imperial-age)
 	(goal RESEARCHING-IMPERIAL MIDDLE-IMPERIAL)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research imperial-age)
	(chat-to-player every-ally "39 I'm advancing to the imperial-age now!")
	(chat-local-to-self "advance to IMPERIAL-AGE")
	(set-goal ADVANCING-TO-AGE IMPERIAL-AGE)
	(enable-timer T-ABLE-TO-PAY-RESOURCES 15)
)
;1000 FOOD, 800 GOLD
(defrule
 	(current-age == castle-age)
	(game-time > IMPERIAL-RESEARCH-TIME-LATE-IMPERIAL)
 	(can-research-with-escrow imperial-age)
 	(goal RESEARCHING-IMPERIAL LATE-IMPERIAL)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research imperial-age)
	(chat-to-player every-ally "39 I'm advancing to the imperial-age now!")
	(chat-local-to-self "advance to IMPERIAL-AGE")
	(set-goal ADVANCING-TO-AGE IMPERIAL-AGE)
	(enable-timer T-ABLE-TO-PAY-RESOURCES 15)
)

;***********************************************
;***********************************************
#else		;if not game type == wonder race

(defrule
	(can-research-with-escrow feudal-age)
	(or
		(players-current-age every-human >= feudal-age)
		(difficulty <= moderate)
	)
=>
	(chat-local-to-self "advance to FEUDAL-AGE")
	(release-escrow food)
	(research feudal-age)
	(set-goal ADVANCING-TO-AGE FEUDAL-AGE)
	(enable-timer T-ABLE-TO-PAY-RESOURCES 15)
)

(defrule
	(can-research-with-escrow castle-age)
	(or
		(players-current-age every-human >= castle-age)
		(difficulty <= moderate)
	)
=>
	(chat-local-to-self "advance to CASTLE-AGE")
	(release-escrow food)
	(release-escrow gold)
	(research castle-age)
	(set-goal ADVANCING-TO-AGE CASTLE-AGE)
	(enable-timer T-ABLE-TO-PAY-RESOURCES 15)
)

(defrule
	(can-research-with-escrow imperial-age)
	(or
		(players-current-age every-human == imperial-age)
		(difficulty <= moderate)
	)
=>
	(chat-local-to-self "advance to IMPERIAL-AGE")
	(release-escrow food)
	(release-escrow gold)
	(research imperial-age)
	(set-goal ADVANCING-TO-AGE IMPERIAL-AGE)
	(enable-timer T-ABLE-TO-PAY-RESOURCES 15)
)
;***********************************************
#end-if
