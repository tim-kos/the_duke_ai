;************************************************************************************************************
;************************************************************************************************************
;BUYING RESOURCES



#load-if-not-defined WONDER-RACE

;BUYING FOOD
;---------------------



;Rule1: buy FOOD to advance to the imperial-age part 1

(defrule
	(food-amount < 1000)
	(gold-amount > 950)
	(commodity-buying-price food < 200)
	(current-age == castle-age)
	(can-buy-commodity food)
	(up-research-status c: imperial-age < research-pending)
	(or
		(and
			(difficulty == moderate)
			(game-time > IMPERIAL-RESEARCH-TIME-MODERATE)
		)
		(and
			(goal RESEARCHING-IMPERIAL FAST-IMPERIAL)
			(game-time > IMPERIAL-RESEARCH-TIME-FAST-IMPERIAL)
		)
	)
=>
	(release-escrow gold)
	(buy-commodity food)
	(chat-local-to-self "Buy food.")
)

(defrule
	(food-amount < 1000)
	(gold-amount > 950)
	(commodity-buying-price food < 200)
	(current-age == castle-age)
	(can-buy-commodity food)
	(up-research-status c: imperial-age < research-pending)
	(or
		(and
			(goal RESEARCHING-IMPERIAL MIDDLE-IMPERIAL)
			(game-time > IMPERIAL-RESEARCH-TIME-MIDDLE-IMPERIAL)
		)
		(and
			(goal RESEARCHING-IMPERIAL LATE-IMPERIAL)
			(game-time > IMPERIAL-RESEARCH-TIME-LATE-IMPERIAL)
		)
	)
=>
	(release-escrow gold)
	(buy-commodity food)
	(chat-local-to-self "Buy food.")
)

(defrule
	(current-age >= castle-age)
  (gold-amount > 1500)
  (food-amount < 700)
  (can-buy-commodity food)
  (commodity-buying-price food < 200)
=>
	(release-escrow gold)
  (buy-commodity food)
  (chat-local-to-self "Buy food.")
)
;----------------------



;Rule12: buy WOOD to balance the economy in the imperial-age part 2

(defrule
	(or
		(up-research-status c: castle-age >= research-pending)
		(current-age >= castle-age)
	)
  (gold-amount > 300)
  (wood-amount < 175)
	(building-type-count-total barracks < 4)
	(building-type-count barracks > 2)
  (can-buy-commodity wood)
  (commodity-buying-price wood < 175)
	(not (goal RUSH-CONTROL NO))
	(goal G-PRIMARY-UNIT eagle-warrior-line)
=>
	(release-escrow gold)
  (buy-commodity wood)
  (chat-local-to-self "Buy wood.")
)

(defrule
	(current-age == imperial-age)
	(gold-amount > 2000)
  (wood-amount < 400)
  (can-buy-commodity wood)
  (commodity-buying-price wood < 175)
=>
	(release-escrow gold)
  (buy-commodity wood)
  (chat-local-to-self "Buy wood.")
)

(defrule
	(current-age == imperial-age)
	(gold-amount > 1400)
  (wood-amount < 300)
  (can-buy-commodity wood)
=>
	(release-escrow gold)
  (buy-commodity wood)
  (chat-local-to-self "Buy wood.")
)
;----------------------



;******************************

;Rule16: buy STONE to build more watch-towers and castles when we aren't advancing to the imperial-age

(defrule
	(current-age == imperial-age)
 	(gold-amount > 700)
  (stone-amount < 100)
  (can-buy-commodity stone)
  (commodity-buying-price stone < 200)
	(building-type-count-total town-center < town-center-building-imperial)
=>
	(release-escrow gold)
  (buy-commodity stone)
  (chat-local-to-self "Buy stone.")
)

(defrule
	(current-age >= castle-age)
 	(gold-amount > 1500)
  (stone-amount < 650)
  (can-buy-commodity stone)
  (commodity-buying-price stone < 200)
	(building-type-count-total castle < castle-building-imperial)
=>
	(release-escrow gold)
  (buy-commodity stone)
  (chat-local-to-self "Buy stone.")
)

(defrule
	(current-age >= castle-age)
 	(gold-amount > 20000)
  (stone-amount < 650)
  (can-buy-commodity stone)
  ;(commodity-buying-price stone < 200) ;If gold is super high, still sell it even at a high price, to ramp up castles
	(building-type-count-total castle < castle-building-imperial)
=>
	(release-escrow gold)
  (buy-commodity stone)
  (chat-local-to-self "Buy stone.")
)

;************************************************************************************************************
;SELLING RESOURCES



;SELLING FOOD FOR GOLD
;-------------------------------------



;Rule19: sell FOOD to research the imperial-age part 1

(defrule
	(food-amount > 1150)
	(gold-amount < 800)
	(current-age == castle-age)
	(can-sell-commodity food)
	(up-research-status c: imperial-age < research-pending)
	(or
		(and
			(difficulty == moderate)
			(game-time > IMPERIAL-RESEARCH-TIME-MODERATE)
		)
		(and
			(goal RESEARCHING-IMPERIAL FAST-IMPERIAL)
			(game-time > IMPERIAL-RESEARCH-TIME-FAST-IMPERIAL)
		)
	)
=>
	(release-escrow food)
	(sell-commodity food)
	(chat-local-to-self "Sell food.")
)

(defrule
	(food-amount > 1150)
	(gold-amount < 800)
	(current-age == castle-age)
	(can-sell-commodity food)
	(up-research-status c: imperial-age < research-pending)
	(or
		(and
			(goal RESEARCHING-IMPERIAL MIDDLE-IMPERIAL)
			(game-time > IMPERIAL-RESEARCH-TIME-MIDDLE-IMPERIAL)
		)
		(and
			(goal RESEARCHING-IMPERIAL LATE-IMPERIAL)
			(game-time > IMPERIAL-RESEARCH-TIME-LATE-IMPERIAL)
		)
	)
=>
	(release-escrow food)
	(sell-commodity food)
	(chat-local-to-self "Sell food.")
)


;Rule19: sell FOOD to get gold in the imperial-age part 1

(defrule
	(food-amount > 1700)
	(gold-amount < 800)
	(can-sell-commodity food)
	(up-research-status c: imperial-age < research-pending)
=>
	(release-escrow food)
	(sell-commodity food)
	(chat-local-to-self "Sell food.")
)

(defrule
	(food-amount > 10000)
	(gold-amount < 1500)
	(can-sell-commodity food)
=>
	(release-escrow food)
	(sell-commodity food)
	(chat-local-to-self "Sell food.")
)

;(defrule
;	(food-amount > 400)
;	(research-completed ri-bow-saw)
;	(gold-amount < 200)
;	(current-age == castle-age)
;	(can-sell-commodity food)
;=>
;	(release-escrow food)
;	(sell-commodity food)
;	(chat-local-to-self "Sell food.")
;)
;---------------------------------------



;Rule29: sell WOOD to advance to the imperial-age part 1

(defrule
	(wood-amount > 500)
	(research-completed ri-bow-saw)
	(gold-amount < 800)
	(current-age == castle-age)
	(can-sell-commodity wood)
	(up-research-status c: imperial-age < research-pending)
	(or
		(and
			(difficulty == moderate)
			(game-time > IMPERIAL-RESEARCH-TIME-MODERATE)
		)
		(and
			(goal RESEARCHING-IMPERIAL FAST-IMPERIAL)
			(game-time > IMPERIAL-RESEARCH-TIME-FAST-IMPERIAL)
		)
	)
=>
	(sell-commodity wood)
	(release-escrow wood)
	(chat-local-to-self "Sell wood.")
)

(defrule
	(wood-amount > 500)
	(research-completed ri-bow-saw)
	(gold-amount < 800)
	(current-age == castle-age)
	(can-sell-commodity wood)
	(up-research-status c: imperial-age < research-pending)
	(or
		(and
			(goal RESEARCHING-IMPERIAL MIDDLE-IMPERIAL)
			(game-time > IMPERIAL-RESEARCH-TIME-MIDDLE-IMPERIAL)
		)
		(and
			(goal RESEARCHING-IMPERIAL LATE-IMPERIAL)
			(game-time > IMPERIAL-RESEARCH-TIME-LATE-IMPERIAL)
		)
	)
=>
	(sell-commodity wood)
	(release-escrow wood)
	(chat-local-to-self "Sell wood.")
)

;(defrule
;	(wood-amount > 400)
;	(research-completed ri-bow-saw)
;	(gold-amount < 200)
;	(current-age == castle-age)
;	(can-sell-commodity wood)
;=>
;	(sell-commodity wood)
;	(release-escrow wood)
;	(chat-local-to-self "Sell wood.")
;)

(defrule
	(current-age == castle-age)
	(research-completed ri-bow-saw)
	(food-amount < 250)
	(gold-amount > 350)
	(goal RUSH-CONTROL RUSHING)
	(goal G-PRIMARY-UNIT eagle-warrior-line)
	(can-buy-commodity food)
=>
	(release-escrow gold)
	(buy-commodity food)
	(chat-local-to-self "Buy food.")
)

(defrule
	(current-age == castle-age)
	(research-completed ri-bow-saw)
	(wood-amount > 200)
	(gold-amount < 200)
	(goal RUSH-CONTROL RUSHING)
	(goal G-PRIMARY-UNIT eagle-warrior-line)
	(can-sell-commodity wood)
=>
	(sell-commodity wood)
	(release-escrow wood)
	(chat-local-to-self "Sell wood.")
)

(defrule			;If
	(wood-amount > 2000)
  (gold-amount < 800)
  (can-sell-commodity wood)
	(current-age == imperial-age)
	(commodity-selling-price wood >= 20)
=>
  (release-escrow wood)
  (sell-commodity wood)
  (chat-local-to-self "Sell wood.")
)

;Rule37: sell WOOD to balance the economy in the imperial-age part 2 (too less gold)

(defrule			;If
	(wood-amount > 2000)
  (gold-amount < 1500)
  (can-sell-commodity wood)
  (current-age >= castle-age)
=>
  (release-escrow wood)
  (sell-commodity wood)
  (chat-local-to-self "Sell wood.")
)

(defrule
	(wood-amount > 450)
	(gold-amount < 200)
	(current-age == castle-age)
	(can-sell-commodity wood)
	(up-research-status c: imperial-age < research-pending)
	(goal RUSH-CONTROL RUSHING)
=>
	(release-escrow wood)
	(sell-commodity wood)
	(chat-local-to-self "Sell wood.")
)
;---------------------------------------------------------------------------------



;Rule49: sell STONE to balance the economy in the imperial-age part 3 (too less gold)

(defrule
	(can-sell-commodity stone)
	(or
		(and
			(current-age == feudal-age)
			(goal RUSH-CONTROL CASTLE)
		)
		(and
			(current-age == castle-age)
			(goal RUSH-CONTROL RUSHING)
		)
	)
=>
	(release-escrow stone)
	(sell-commodity stone)
	(sell-commodity stone)
	(chat-local-to-self "Sell stone.")
	(disable-self)
)

(defrule
	(can-sell-commodity wood)
	(current-age == feudal-age)
	(goal RUSH-CONTROL CASTLE)
	(gold-amount < 200)
	(building-type-count-total blacksmith > 0)
	(up-research-status c: castle-age < research-pending)
=>
	(release-escrow wood)
	(sell-commodity wood)
	(chat-local-to-self "Sell wood.")
	(disable-self)
)

(defrule
	(can-sell-commodity wood)
	(current-age == feudal-age)
	(goal RUSH-CONTROL CASTLE)
	(food-amount > 900)
	(gold-amount < 200)
	(building-type-count-total blacksmith > 0)
	(up-research-status c: castle-age < research-pending)
=>
	(release-escrow food)
	(sell-commodity food)
	(chat-local-to-self "Sell food.")
	(disable-self)
)

#load-if-defined DEATH-MATCH


(defrule
	(stone-amount > 1500)
	(current-age == imperial-age)
	(can-sell-commodity stone)
	(commodity-selling-price stone > 60)
=>
	(release-escrow stone)
	(sell-commodity stone)
)


#end-if		;if game type != deathmatch


#end-if		;if not game type == wonder race



;************************************************************************
;************************************************************************



#load-if-defined WONDER-RACE


;SOME STUFF TO BUILD A WONDER
;----------------------------------------------------



;Rule50: buy FOOD to advance to the castle-age part 1

(defrule
	(food-amount < 800)
	(gold-amount > 350)
	(building-type-count market >= 1)
	(commodity-buying-price food < 200)
	(current-age == feudal-age)
	(can-buy-commodity food)
	(not (up-research-status c: castle-age >= research-pending))
=>
	(release-escrow gold)
	(buy-commodity food)
	(chat-local-to-self "Buy food.")
)

(defrule
	(food-amount < 1000)
	(gold-amount > 900)
	(building-type-count market >= 1)
	(commodity-buying-price food < 200)
	(current-age == castle-age)
	(can-buy-commodity food)
	(up-research-status c: imperial-age < research-pending)
=>
	(release-escrow gold)
	(buy-commodity food)
	(chat-local-to-self "Buy food.")
)

(defrule			;If
	(current-age == imperial-age)
	(wood-amount < 1000)
	(gold-amount > 1100)
	(can-buy-commodity wood)
	(building-type-count market >= 1)
	(commodity-buying-price wood < 200)
=>
	(release-escrow gold)
	(buy-commodity wood)
	(chat-local-to-self "Buy wood.")
)

(defrule			;If
	(current-age == imperial-age)
	(wood-amount >= 1000)
	(stone-amount < 1000)
	(gold-amount > 1100)
	(can-buy-commodity stone)
	(building-type-count market >= 1)
	(commodity-buying-price stone < 200)
=>
	(release-escrow gold)
	(buy-commodity stone)
	(chat-local-to-self "Buy stone.")
)

(defrule			;If
	(current-age == imperial-age)
	(food-amount > 200)
	(gold-amount < 1200)
	(can-sell-commodity food)
	(building-type-count-total wonder < 1)
=>
;	(chat-local-to-self "sell FOOD for building a wonder")
	(sell-commodity food)
	(chat-local-to-self "Sell food.")
)

(defrule			;If
	(current-age == imperial-age)
	(wood-amount > 1100)
	(gold-amount < 1000)
	(can-sell-commodity wood)
=>
;	(chat-local-to-self "sell WOOD for building a wonder")
	(sell-commodity wood)
	(chat-local-to-self "Sell wood.")
)

(defrule			;If
	(current-age == imperial-age)
	(stone-amount > 1100)
	(gold-amount < 1100)
	(can-sell-commodity stone)
=>
;	(chat-local-to-self "sell STONE for building a wonder")
	(sell-commodity stone)
	(chat-local-to-self "Sell stone.")
)

(defrule
	(wood-amount > 200)
	(not (research-available ri-double-bit-axe))
	(or
		(and
			(food-amount < 800)
			(gold-amount < 350)
		)
		(gold-amount < 200)
	)
	(building-type-count-total market > 0)
	(building-type-count-total blacksmith > 0)
	(current-age == feudal-age)
	(can-sell-commodity wood)
	(not (up-research-status c: castle-age >= research-pending))
=>
	(release-escrow wood)
	(sell-commodity wood)
	(chat-local-to-self "Sell wood.")
)

;Rule60: sell STONE to advance to the imperial-age

(defrule
	(stone-amount > 1100)
	(gold-amount < 1000)
	(building-type-count market >= 1)
	(current-age == castle-age)
	(can-sell-commodity stone)
	(up-research-status c: imperial-age < research-pending)
=>
	(release-escrow stone)
	(sell-commodity stone)
	(chat-local-to-self "Sell stone.")
)


#end-if		;if game type == wonder race



;*****************************************************************
;*****************************************************************
;TRIBUTES



(defrule
	(building-type-count-total market > 0)
	(player-in-game any-ally)
	(cc-players-building-type-count every-ally market < 1)
	(players-current-age any-ally > dark-age)
	(players-current-age-time any-ally > 300)
=>
	(chat-to-player this-any-ally "39 Please build a market to trade with me!")
	(disable-self)
)
;--------------------------------------------------------



#load-if-not-defined WONDER-RACE

(defrule
	(players-building-type-count any-ally market > 0)
=>
	(enable-timer T-TRIBUTE 1)
	(disable-self)
)
;----------------------------



(defrule
	(current-age >= imperial-age)
	(timer-triggered T-TRIBUTE)
	(player-in-game any-ally)
	(goal RESOURCE-NEEDED FOOD)
=>
	(chat-to-player every-ally "3 I need some food!")
	(chat-to-player every-ally "39")
)



(defrule				; If
	(players-tribute-memory any-ally food >= 1)
=>
	(clear-tribute-memory this-any-ally food)
	(chat-to-player this-any-ally "39 Thanks for your food!")
)
;-----------------------------



(defrule
	(current-age >= imperial-age)
	(timer-triggered T-TRIBUTE)
	(player-in-game any-ally)
	(goal RESOURCE-NEEDED WOOD)
=>
	(chat-to-player every-ally "4 I need some wood!")
	(chat-to-player every-ally "39")
)



(defrule
	(players-tribute-memory any-ally wood >= 1)
=>
	(clear-tribute-memory this-any-ally wood)
	(chat-to-player this-any-ally "39 Thanks for your wood!")
)
;----------------------------



(defrule
	(current-age >= imperial-age)
	(timer-triggered T-TRIBUTE)
	(player-in-game any-ally)
	(goal RESOURCE-NEEDED GOLD)
=>
	(chat-to-player every-ally "5 I need some gold!")
	(chat-to-player every-ally "39")
)



(defrule
	(players-tribute-memory any-ally gold >= 1)
=>
	(clear-tribute-memory this-any-ally gold)
	(chat-to-player this-any-ally "39 Thanks for your gold!")
)
;------------------------------



(defrule
	(current-age >= imperial-age)
	(timer-triggered T-TRIBUTE)
	(player-in-game any-ally)
	(stone-amount < 700)
	(goal RESOURCE-NEEDED NO)
=>
	(chat-to-player every-ally "6 I need some stone!")
	(chat-to-player every-ally "39")
)



(defrule
	(players-tribute-memory any-ally stone >= 1)
=>
	(clear-tribute-memory this-any-ally stone)
	(chat-to-player this-any-ally "39 Thanks for your stone!")
)

#end-if		;if not game-type == wonder-race



;THE ENEMY ASK US FOR RESOURCES
;-------------------------------------------------------



(defrule
	(or
		(or
			(or
				(taunt-detected any-enemy 3)
				(taunt-detected any-enemy 4)
			)
			(taunt-detected any-enemy 5)
		)
		(taunt-detected any-enemy 6)
	)
=>
	(acknowledge-taunt this-any-enemy 3)
	(acknowledge-taunt this-any-enemy 4)
	(acknowledge-taunt this-any-enemy 5)
	(acknowledge-taunt this-any-enemy 6)
	(chat-to-player this-any-enemy "40 No resources for you! You aren't my ally!")
)
;-----------------------------------------------------------



(defrule
	(or
		(or
			(or
				(taunt-detected any-neutral 3)
				(taunt-detected any-neutral 4)
			)
			(taunt-detected any-neutral 5)
		)
		(taunt-detected any-neutral 6)
	)
=>
	(acknowledge-taunt this-any-neutral 3)
	(acknowledge-taunt this-any-neutral 4)
	(acknowledge-taunt this-any-neutral 5)
	(acknowledge-taunt this-any-neutral 6)
	(chat-to-player this-any-neutral "41 No resources for you! You aren't my ally!")
)
;-------------------------------------------------------



#load-if-not-defined WONDER-RACE


(defrule
	(or
		(or
			(or
				(taunt-detected any-ally 3)
				(taunt-detected any-ally 4)
			)
			(taunt-detected any-ally 5)
		)
		(taunt-detected any-ally 6)
	)
	(building-type-count-total market < 1)
=>
	(acknowledge-taunt this-any-ally 3)
	(acknowledge-taunt this-any-ally 4)
	(acknowledge-taunt this-any-ally 5)
	(acknowledge-taunt this-any-ally 6)
	(chat-to-player this-any-ally "39 Sorry, I haven't got any market yet!")
)



(defrule
	(taunt-detected any-ally 3)
	(food-amount < 400)
=>
	(acknowledge-taunt this-any-ally 3)
	(chat-to-player this-any-ally "39 Sorry, I can't give you food at this moment, because I haven't got so much food!")
)



(defrule
	(taunt-detected any-ally 4)
	(wood-amount < 400)
=>
	(acknowledge-taunt this-any-ally 4)
	(chat-to-player this-any-ally "39 Sorry, I can't give you wood at this moment, because I haven't got so much wood!")
)



(defrule
	(taunt-detected any-ally 5)
	(gold-amount < 500)
=>
	(acknowledge-taunt this-any-ally 5)
	(chat-to-player this-any-ally "39 Sorry, I can't give you gold at this moment; because I haven't got so much gold!")
)



(defrule
	(taunt-detected any-ally 6)
	(building-type-count castle < 1)
=>
	(acknowledge-taunt this-any-ally 6)
	(chat-to-player this-any-ally "39 Sorry, I can't give you stones at this moment, because I haven't built a castle yet!")
	(chat-to-player this-any-ally "I must do that first to run properly!")
)



(defrule
	(or
		(or
			(or
				(taunt-detected any-computer-ally 3)
				(taunt-detected any-computer-ally 4)
			)
			(taunt-detected any-computer-ally 5)
		)
		(taunt-detected any-computer-ally 6)
	)
	(not (research-completed ri-coinage))
=>
	(acknowledge-taunt this-any-computer-ally 3)
	(acknowledge-taunt this-any-computer-ally 4)
	(acknowledge-taunt this-any-computer-ally 5)
	(acknowledge-taunt this-any-computer-ally 6)
	(chat-to-player this-any-computer-ally "39 Sorry, I did not research the coinage yet! Tributes are too expensive now!")
)
;HUMAN ALLIES ASK US FOR RESOURCES



(defrule
	(taunt-detected any-human-ally 3)
	(food-amount >= 400)
	(goal UNDER-ATTACK NO)
	(building-type-count-total market > 0)
=>
	(acknowledge-taunt this-any-human-ally 3)
	(tribute-to-player this-any-human-ally food 100)
	(chat-to-player this-any-human-ally "39 Here is some food for you! Use it for training villagers")
)



(defrule
	(taunt-detected any-human-ally 4)
	(wood-amount >= 400)
	(goal UNDER-ATTACK NO)
	(building-type-count-total market > 0)
=>
	(acknowledge-taunt this-any-human-ally 4)
	(tribute-to-player this-any-human-ally wood 100)
	(chat-to-player this-any-human-ally "39 Here is some wood for you! Use it to build up your kingdom")
)



(defrule
	(taunt-detected any-human-ally 5)
	(gold-amount >= 500)
	(goal UNDER-ATTACK NO)
	(building-type-count-total market > 0)
=>
	(acknowledge-taunt this-any-human-ally 5)
	(tribute-to-player this-any-human-ally gold 100)
	(chat-to-player this-any-human-ally "39 Here is some gold for you! Use it for training better units!")
)



(defrule
	(taunt-detected any-human-ally 6)
	(stone-amount >= 500)
	(goal UNDER-ATTACK NO)
	(building-type-count-total market > 0)
=>
	(acknowledge-taunt this-any-human-ally 6)
	(tribute-to-player this-any-human-ally stone 100)
	(chat-to-player this-any-human-ally "39 Here are some stones for you! Use them for building more castles!")
)
;COMPUTER ALLIES ASK US FOR RESOURCES



(defrule
	(taunt-detected any-computer-ally 3)
	(food-amount >= 400)
	(goal UNDER-ATTACK NO)
	(building-type-count-total market > 0)
	(research-completed ri-coinage)
=>
	(acknowledge-taunt this-any-computer-ally 3)
	(tribute-to-player this-any-computer-ally food 100)
	(chat-to-player this-any-computer-ally "39 Here is some food for you! Use it for training villagers")
)



(defrule
	(taunt-detected any-computer-ally 4)
	(wood-amount >= 400)
	(goal UNDER-ATTACK NO)
	(building-type-count-total market > 0)
	(research-completed ri-coinage)
=>
	(acknowledge-taunt this-any-computer-ally 4)
	(tribute-to-player this-any-computer-ally wood 100)
	(chat-to-player this-any-computer-ally "39 Here is some wood for you! Use it to build up your kingdom")
)



(defrule
	(taunt-detected any-computer-ally 5)
	(gold-amount >= 500)
	(goal UNDER-ATTACK NO)
	(building-type-count-total market > 0)
	(research-completed ri-coinage)
=>
	(acknowledge-taunt this-any-computer-ally 5)
	(tribute-to-player this-any-computer-ally gold 100)
	(chat-to-player this-any-computer-ally "39 Here is some gold for you! Use it for training better units!")
)



(defrule
	(taunt-detected any-computer-ally 6)
	(building-type-count castle > 0)
	(goal UNDER-ATTACK NO)
	(building-type-count-total market > 0)
	(research-completed ri-coinage)
=>
	(acknowledge-taunt this-any-computer-ally 6)
	(tribute-to-player this-any-computer-ally stone 100)
	(chat-to-player this-any-computer-ally "39 Here are some stones for you! Use them for building more castles!")
)

#end-if		;if not game type == wonder race



#load-if-defined WONDER-RACE

(defrule
	(or
		(or
			(or
				(taunt-detected any-ally 3)
				(taunt-detected any-ally 4)
			)
			(taunt-detected any-ally 5)
		)
		(taunt-detected any-ally 6)
	)
=>
	(acknowledge-taunt this-any-ally 3)
	(acknowledge-taunt this-any-ally 4)
	(acknowledge-taunt this-any-ally 5)
	(acknowledge-taunt this-any-ally 6)
	(chat-to-player this-any-ally "I will not give you any resources, because I want to win that wonder race!")
)

#end-if		;if game type == wonder race



(defrule
	(timer-triggered T-TRIBUTE)
	(not (player-in-game any-human-ally) )
=>
	(disable-timer T-TRIBUTE)
	(enable-timer T-TRIBUTE 120)
)



(defrule
	(timer-triggered T-TRIBUTE)
	(player-in-game any-human-ally)
=>
	(disable-timer T-TRIBUTE)
	(enable-timer T-TRIBUTE 240)
)
