;************************************************************************************************************
;************************************************************************************************************
;UPGRADES FOR MILITARY UNITS


(defrule
	(true)
=>
	(set-goal G-ABLE-TO-UPGRADE-INFANTRY NO)
	(set-goal G-ABLE-TO-UPGRADE-ARCHERS NO)
	(set-goal G-ABLE-TO-UPGRADE-CAVALRY NO)
)

#load-if-not-defined DEATH-MATCH

;*******************************************************************
;*******************************************************************
;CAVALRY

;***********************************************
;***********************************************
;CAVALRY-UPGRADES IN FEUDAL-AGE

;Rule66: define when can we upgrade our CAVALRY in feudal-age

(defrule
	(unit-type-count-total scout-cavalry-line >= THREE-PERCENT-POP)
	(food-amount >= 1000)
	(gold-amount >= 400)
	(current-age == feudal-age)
	(not (goal RUSH-CONTROL RUSHING))
=>
	(set-goal G-ABLE-TO-UPGRADE-CAVALRY HIGH)
)

(defrule
	(unit-type-count-total scout-cavalry-line >= FOUR-PERCENT-POP)
	(current-age == feudal-age)
	(goal RUSH-CONTROL RUSHING)
=>
	(set-goal G-ABLE-TO-UPGRADE-CAVALRY HIGH)
)
;***********************************************
;CAVALRY-UPGRADES IN CASTLE-AGE



;Rule70: define when can we upgrade our CAVALRY in castle-age part 2; we do not do any rush (upgrading)

(defrule
	(unit-type-count-total cavalry-class >= TWO-PERCENT-POP)
	(current-age == castle-age)
	(not (goal RUSH-CONTROL RUSHING))
=>
	(set-goal G-ABLE-TO-UPGRADE-CAVALRY LOW)
)

(defrule
	(unit-type-count-total cavalry-class >= FOUR-PERCENT-POP)
	(current-age == castle-age)
	(not (goal RUSH-CONTROL RUSHING))
=>
	(set-goal G-ABLE-TO-UPGRADE-CAVALRY HIGH)
)

(defrule
	(unit-type-count-total cavalry-class >= TWO-PERCENT-POP)
	(current-age == castle-age)
	(goal RUSH-CONTROL RUSHING)
	(goal RUSH-ATTACK-STARTED YES)
=>
	(set-goal G-ABLE-TO-UPGRADE-CAVALRY LOW)
)

(defrule
	(unit-type-count-total cavalry-class >= THREE-PERCENT-POP)
	(current-age == castle-age)
	(goal RUSH-CONTROL RUSHING)
	(goal RUSH-ATTACK-STARTED YES)
=>
	(set-goal G-ABLE-TO-UPGRADE-CAVALRY HIGH)
)
;***********************************************
;CAVALRY-UPGRADES IN IMPERIAL-AGE



;Rule70: define when can we upgrade our CAVALRY in castle-age part 2; we do not do any rush (upgrading)

(defrule
	(unit-type-count-total cavalry-class >= TWO-PERCENT-POP)
	(current-age == imperial-age)
	(not (goal RUSH-CONTROL RUSHING))
=>
	(set-goal G-ABLE-TO-UPGRADE-CAVALRY LOW)
)

(defrule
	(unit-type-count-total cavalry-class >= THREE-PERCENT-POP)
	(current-age == imperial-age)
	(not (goal RUSH-CONTROL RUSHING))
=>
	(set-goal G-ABLE-TO-UPGRADE-CAVALRY HIGH)
)


;*******************************************************************
;INFANTRY



;***********************************
;***********************************
;militiaman-line-UPGRADES IN FEUDAL-AGE


;Rule43: start the lopp of upgrading our militiaman-line

(defrule
	(current-age == feudal-age)
	(or
		(and
			(unit-type-count-total infantry-class >= THREE-PERCENT-POP)
			(not (goal RUSH-CONTROL RUSHING))
		)
		(and
			(unit-type-count-total infantry-class >= FIVE-PERCENT-POP)
			(goal RUSH-CONTROL RUSHING)
		)
	)
=>
	(set-goal G-ABLE-TO-UPGRADE-INFANTRY HIGH)
)

;***********************************
;militiaman-line-UPGRADES IN CASTLE-AGE



;Rule46: upgrade militiaman-line in castle-age if we need upgrades and have got enough resources and do not do any rush

(defrule
	(current-age == castle-age)
	(unit-type-count-total infantry-class >= ONE-PERCENT-POP)
	(not (goal RUSH-CONTROL RUSHING))
=>
	(set-goal G-ABLE-TO-UPGRADE-INFANTRY LOW)
)



(defrule
	(current-age == castle-age)
	(unit-type-count-total infantry-class >= TWO-PERCENT-POP)
	(not (goal RUSH-CONTROL RUSHING))
=>
	(set-goal G-ABLE-TO-UPGRADE-INFANTRY MIDDLE)
)

(defrule
	(current-age == castle-age)
	(or
		(or
			(unit-type-count-total militiaman-line >= TWO-PERCENT-POP)
			(unit-type-count-total eagle-warrior-line >= THREE-PERCENT-POP)
		)
		(unit-type-count-total spearman-line >= TWO-PERCENT-POP)
	)
	(goal RUSH-CONTROL RUSHING)
	(goal RUSH-ATTACK-STARTED YES)
=>
	(set-goal G-ABLE-TO-UPGRADE-INFANTRY LOW)
)

(defrule
	(current-age == castle-age)
	(or
		(or
			(unit-type-count-total militiaman-line >= THREE-PERCENT-POP)
			(unit-type-count-total eagle-warrior-line >= FOUR-PERCENT-POP)
		)
		(unit-type-count-total spearman-line >= THREE-PERCENT-POP)
	)
	(goal RUSH-CONTROL RUSHING)
	(goal RUSH-ATTACK-STARTED YES)
=>
	(set-goal G-ABLE-TO-UPGRADE-INFANTRY MIDDLE)
)
;***********************************
;militiaman-line-UPGRADES IN IMPERIAL-AGE



;Rule46: upgrade militiaman-line in castle-age if we need upgrades and have got enough resources and do not do any rush

(defrule
	(current-age == imperial-age)
	(unit-type-count-total infantry-class >= ONE-PERCENT-POP)
=>
	(set-goal G-ABLE-TO-UPGRADE-INFANTRY LOW)
)
(defrule
	(current-age == imperial-age)
	(unit-type-count-total infantry-class >= TWO-PERCENT-POP)
=>
	(set-goal G-ABLE-TO-UPGRADE-INFANTRY MIDDLE)
)
(defrule
	(current-age == imperial-age)
	(unit-type-count-total infantry-class >= THREE-PERCENT-POP)
=>
	(set-goal G-ABLE-TO-UPGRADE-INFANTRY HIGH)
)

;*******************************************************************
;ARCHERS



;***********************************************
;***********************************************
;ARCHER-UPGRADES IN FEUDAL-AGE

(defrule					
	(current-age == feudal-age)
	(or
		(unit-type-count-total archer-line >= FOUR-PERCENT-POP)
		(unit-type-count-total skirmisher-line >= FOUR-PERCENT-POP)
	)
	(food-amount >= 900)
	(gold-amount >= 300)
	(current-age == feudal-age)
	(not (goal RUSH-CONTROL RUSHING))
=>					;then
	(set-goal G-ABLE-TO-UPGRADE-ARCHERS HIGH)
)

(defrule
	(or
		(unit-type-count-total archer-line >= FIVE-PERCENT-POP)
		(unit-type-count-total skirmisher-line >= FIVE-PERCENT-POP)
	)
	(current-age == feudal-age)
	(goal RUSH-CONTROL RUSHING)
=>
	(set-goal G-ABLE-TO-UPGRADE-ARCHERS HIGH)
)
;***********************************************
;ARCHER-UPGRADES IN CASTLE-AGE



;Rule56: make sure if it is possible we can upgrade our archers (is our primary or secondary unit foot ranged unit)

(defrule
	(or
		(or
			(unit-type-count-total archery-class >= TWO-PERCENT-POP)
			(unit-type-count-total cavalry-archer-class >= TWO-PERCENT-POP)
		)
		(or
			(unit-type-count-total cavalry-cannon-class >= TWO-PERCENT-POP)
			(unit-type-count-total archery-cannon-class >= TWO-PERCENT-POP)
		)
	)
	(not (goal RUSH-CONTROL RUSHING))
	(current-age == castle-age)
=>
	(set-goal G-ABLE-TO-UPGRADE-ARCHERS LOW)
)

(defrule
	(or
		(or
			(unit-type-count-total archery-class >= THREE-PERCENT-POP)
			(unit-type-count-total cavalry-archer-class >= THREE-PERCENT-POP)
		)
		(or
			(unit-type-count-total cavalry-cannon-class >= THREE-PERCENT-POP)
			(unit-type-count-total archery-cannon-class >= THREE-PERCENT-POP)
		)
	)
	(not (goal RUSH-CONTROL RUSHING))
	(current-age == castle-age)
=>
	(set-goal G-ABLE-TO-UPGRADE-ARCHERS MIDDLE)
)

(defrule
	(or
		(or
			(unit-type-count-total archery-class >= TWO-PERCENT-POP)
			(unit-type-count-total cavalry-archer-class >= TWO-PERCENT-POP)
		)
		(or
			(unit-type-count-total cavalry-cannon-class >= TWO-PERCENT-POP)
			(unit-type-count-total archery-cannon-class >= TWO-PERCENT-POP)
		)
	)
	(current-age == castle-age)
	(goal RUSH-CONTROL RUSHING)
	(goal RUSH-ATTACK-STARTED YES)
=>
	(set-goal G-ABLE-TO-UPGRADE-ARCHERS LOW)
)

(defrule
	(or
		(or
			(unit-type-count-total archery-class >= THREE-PERCENT-POP)
			(unit-type-count-total cavalry-archer-class >= THREE-PERCENT-POP)
		)
		(or
			(unit-type-count-total cavalry-cannon-class >= THREE-PERCENT-POP)
			(unit-type-count-total archery-cannon-class >= THREE-PERCENT-POP)
		)
	)
	(current-age == castle-age)
	(goal RUSH-CONTROL RUSHING)
	(goal RUSH-ATTACK-STARTED YES)
=>
	(set-goal G-ABLE-TO-UPGRADE-ARCHERS MIDDLE)
)
;***********************************************
;ARCHER-UPGRADES IN IMPERIAL-AGE

(defrule
	(or
		(or
			(unit-type-count-total archery-class >= ONE-PERCENT-POP)
			(unit-type-count-total cavalry-archer-class >= ONE-PERCENT-POP)
		)
		(or
			(unit-type-count-total cavalry-cannon-class >= ONE-PERCENT-POP)
			(unit-type-count-total archery-cannon-class >= ONE-PERCENT-POP)
		)
	)
	(current-age == imperial-age)
=>
	(set-goal G-ABLE-TO-UPGRADE-ARCHERS LOW)
)

(defrule
	(or
		(or
			(unit-type-count-total archery-class >= TWO-PERCENT-POP)
			(unit-type-count-total cavalry-archer-class >= TWO-PERCENT-POP)
		)
		(or
			(unit-type-count-total cavalry-cannon-class >= TWO-PERCENT-POP)
			(unit-type-count-total archery-cannon-class >= TWO-PERCENT-POP)
		)
	)
	(current-age == imperial-age)
=>
	(set-goal G-ABLE-TO-UPGRADE-ARCHERS MIDDLE)
)

(defrule
	(or
		(or
			(unit-type-count-total archery-class >= THREE-PERCENT-POP)
			(unit-type-count-total cavalry-archer-class >= THREE-PERCENT-POP)
		)
		(or
			(unit-type-count-total cavalry-cannon-class >= THREE-PERCENT-POP)
			(unit-type-count-total archery-cannon-class >= THREE-PERCENT-POP)
		)
	)
	(current-age == imperial-age)
=>
	(set-goal G-ABLE-TO-UPGRADE-ARCHERS HIGH)
)

#else		;if not game-type == death-match
;***********************************************

;***********************************
;***********************************
;militiaman-line-UPGRADES

(defrule
	(unit-type-count-total infantry-class > 0)
=>
	(set-goal G-ABLE-TO-UPGRADE-INFANTRY HIGH)
)


;*******************************************************************
;ARCHER-UPGRADES

(defrule
	(or
		(or
			(unit-type-count-total archery-class > 0)
			(unit-type-count-total cavalry-archer-class > 0)
		)
		(or
			(unit-type-count-total cavalry-cannon-class > 0)
			(unit-type-count-total archery-cannon-class > 0)
		)
	)
=>
	(set-goal G-ABLE-TO-UPGRADE-ARCHERS HIGH)
)


;*******************************************************************
;CAVALRY-UPGRADES

(defrule
	(unit-type-count-total cavalry-class > 0)
=>
	(set-goal G-ABLE-TO-UPGRADE-CAVALRY HIGH)
)


;***********************************************
#end-if		;if game type == death-match


(defrule
	(food-amount > 5000)
	(gold-amount > 5000)
=>
	(set-goal G-ABLE-TO-UPGRADE-INFANTRY HIGH)
	(set-goal G-ABLE-TO-UPGRADE-ARCHERS HIGH)
	(set-goal G-ABLE-TO-UPGRADE-CAVALRY HIGH)
)

;************************************************************************************************************
;************************************************************************************************************
;TECHNOLOGIES IN BLACKSMITH (MILITARY-UNITS-UPGRADES)


;*******************************************************************
;*******************************************************************
;FEUDAL-AGE


;FLETCHING
;100 FOOD, 50 GOLD

(defrule
 	(can-research-with-escrow ri-fletching)
	(or
		(building-type-count castle > 0)
		(unit-type-count-total galley-line >= THREE-PERCENT-POP)
	)
	(goal ALL-ECONOMY-TECH-DONE YES)
	(goal RUSH-CONTROL NO)
=>
	(release-escrow food)
	(release-escrow gold)
	(research ri-fletching)
; (chat-local-to-self "research FLETCHING")
)

(defrule
 	(can-research-with-escrow ri-fletching)
    (or
		(or
			(up-research-status c: castle-age >= research-pending)
			(goal UNDER-ATTACK YES) ;To improve damage of the TC and defending skirmishers
		)
		(and
			(food-amount > 1000)
			(gold-amount > 400)
		)
	)
	(nand
		(goal ALL-ECONOMY-TECH-DONE NO)
		(goal UNDER-ATTACK NO)
	)
	(goal RUSH-CONTROL NO)
=>
	(release-escrow food)
	(release-escrow gold)
	(research ri-fletching)
; (chat-local-to-self "research FLETCHING")
)

(defrule
 	(can-research-with-escrow ri-fletching)
	(goal RUSH-CONTROL RUSHING)
	(goal RUSH-ATTACK-STARTED YES)
=>
	(release-escrow food)
	(release-escrow gold)
	(research ri-fletching)
; (chat-local-to-self "research FLETCHING")
)

;***********************************************
;FORGING
;150 FOOD


(defrule
 	(can-research-with-escrow ri-forging)
	(not (goal G-ABLE-TO-UPGRADE-INFANTRY NO))
	(not (goal G-ABLE-TO-UPGRADE-CAVALRY NO))
	(or
		(soldier-count > TWO-PERCENT-POP)
		(or
			(up-research-status c: castle-age >= research-pending)
			(food-amount > 950)
		)
	)
	(or
		(not (research-available ri-scale-mail))
		(not (research-available ri-scale-barding))
	)
	(or
		(not (research-available ri-plate-mail))
		(not (research-available ri-plate-barding))
	)
	(goal ALL-ECONOMY-TECH-DONE YES)
=>					;then
	(release-escrow food)
 	(research ri-forging)
; (chat-local-to-self "research FORGING")
)

(defrule
 	(can-research-with-escrow ri-forging)
	(or
		(and
			(goal RUSH-CONTROL RUSHING)
			(goal RUSH-ATTACK-STARTED YES)
		)
		(or
			(up-research-status c: castle-age >= research-pending)
			(food-amount > 950)
		)
	)
	(goal ALL-ECONOMY-TECH-DONE YES)
=>					;then
	(release-escrow food)
 	(research ri-forging)
; (chat-local-to-self "research FORGING")
)

(defrule
 	(can-research-with-escrow ri-forging)
	(or
		(goal RUSH-CONTROL CASTLE)
		(and
			(goal RUSH-CONTROL RUSHING)
			(current-age == castle-age)
		)
	)
	(or
		(goal G-PRIMARY-UNIT knight-line)
		(goal G-PRIMARY-UNIT eagle-warrior-line)
	)
	(food-amount > 200)
	(up-research-status c: castle-age >= research-pending)
=>					;then
	(release-escrow food)
 	(research ri-forging)
; (chat-local-to-self "research FORGING")
)

;***********************************************
;SCALE-MAIL
;100 FOOD

(defrule
 	(can-research-with-escrow ri-scale-mail)
	(or
		(and
			(goal RUSH-CONTROL RUSHING)
			(goal RUSH-ATTACK-STARTED YES)
		)
		(or
			(up-research-status c: castle-age >= research-pending)
			(food-amount > 950)
		)
	)
	(goal ALL-ECONOMY-TECH-DONE YES)
=>					;then
	(release-escrow food)
	(research ri-scale-mail)
; (chat-local-to-self "research SCALE-MAIL")
)

(defrule
 	(can-research-with-escrow ri-scale-mail)
	(or
		(goal RUSH-CONTROL CASTLE)
		(and
			(goal RUSH-CONTROL RUSHING)
			(current-age == castle-age)
		)
	)
	(goal G-PRIMARY-UNIT eagle-warrior-line)
	(food-amount > 200)
	(up-research-status c: castle-age >= research-pending)
=>					;then
	(release-escrow food)
 	(research ri-scale-mail)
; (chat-local-to-self "research SCALE-MAIL")
)

;***********************************************
;SCALE-BARDING
;150 FOOD

(defrule
 	(can-research-with-escrow ri-scale-barding)
	(or
		(and
			(goal RUSH-CONTROL RUSHING)
			(goal RUSH-ATTACK-STARTED YES)
		)
		(or
			(up-research-status c: castle-age >= research-pending)
			(food-amount > 950)
		)
	)
	(goal ALL-ECONOMY-TECH-DONE YES)
=>
	(release-escrow food)
 	(research ri-scale-barding)
; (chat-local-to-self "research SCALE-BARDING")
)

(defrule
 	(can-research-with-escrow ri-scale-barding)
	(or
		(goal RUSH-CONTROL CASTLE)
		(and
			(goal RUSH-CONTROL RUSHING)
			(current-age == castle-age)
		)
	)
	(goal G-PRIMARY-UNIT knight-line)
	(up-research-status c: castle-age >= research-pending)
=>
	(release-escrow food)
 	(research ri-scale-barding)
; (chat-local-to-self "research SCALE-BARDING")
)
;***********************************************
;PADDED-ARCHER-ARMOR
;100 FOOD

(defrule
 	(can-research-with-escrow ri-padded-archer-armor)
	(or
		(up-compare-goal G-ABLE-TO-UPGRADE-ARCHERS < HIGH)
		(or
			(goal G-ABLE-TO-UPGRADE-ARCHERS HIGH)
			(food-amount > 1300)
		)
	)
	(or
		(and
			(goal RUSH-CONTROL RUSHING)
			(goal RUSH-ATTACK-STARTED YES)
		)
		(or
			(up-research-status c: castle-age >= research-pending)
			(food-amount > 900)
		)
	)
	(not (research-available ri-fletching))
	(goal ALL-ECONOMY-TECH-DONE YES)
=>
	(release-escrow food)
	(research ri-padded-archer-armor)
; (chat-local-to-self "research PADDED-ARCHER-ARMOR")
)
;*******************************************************************
;CASTLE-AGE


(defrule
 	(can-research-with-escrow ri-arson)
	(or
		(and
			(food-amount > 800)
			(gold-amount > 500)
		)
		(up-compare-goal G-ABLE-TO-UPGRADE-INFANTRY >= MIDDLE)
	)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-arson)
; (chat-local-to-self "research ARSON")
)

;***********************************************
;***********************************************
;IRON-CASTING
;220 FOOD, 120 GOLD

(defrule
 	(can-research-with-escrow ri-iron-casting)
	(or
		(and
			(food-amount > 800)
			(gold-amount > 500)
		)
		(or
			(up-compare-goal G-ABLE-TO-UPGRADE-INFANTRY >= HIGH)
			(up-compare-goal G-ABLE-TO-UPGRADE-CAVALRY >= HIGH)
		)
	)
	(goal ALL-ECONOMY-TECH-DONE YES)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-iron-casting)
; (chat-local-to-self "research IRON-CASTING")
)

;***********************************************
;BODKIN-ARROW
;220 FOOD, 120 GOLD

(defrule
 	(can-research-with-escrow ri-bodkin-arrow)
	(or
		(and
			(food-amount > 800)
			(gold-amount > 500)
		)
		(up-compare-goal G-ABLE-TO-UPGRADE-ARCHERS >= LOW)
	)
	(goal ALL-ECONOMY-TECH-DONE YES)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-bodkin-arrow)
; (chat-local-to-self "research BODKIN-ARROW")
)

(defrule
 	(can-research-with-escrow ri-bodkin-arrow)
	(or
		(building-type-count castle >= 2)
		(unit-type-count-total galley-line >= THREE-PERCENT-POP)
	)
	(goal ALL-ECONOMY-TECH-DONE YES)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-bodkin-arrow)
; (chat-local-to-self "research BOKIN-ARROW")
)

;***********************************************
;CHAIN-BARDING

(defrule
 	(can-research-with-escrow ri-chain-barding)
	(or
		(and
			(food-amount > 800)
			(gold-amount > 500)
		)
		(up-compare-goal G-ABLE-TO-UPGRADE-CAVALRY >= MIDDLE)
	)
	(goal ALL-ECONOMY-TECH-DONE YES)
=>
	(release-escrow food)
	(release-escrow gold)
	(research ri-chain-barding)
; (chat-local-to-self "research CHAIN-BARDING")
)

;***********************************************
;LEATHER-ARCHER-ARMOR
;150 FOOD, 150 GOLD

(defrule
 	(can-research-with-escrow ri-leather-archer-armor)
	(or
		(and
			(food-amount > 800)
			(gold-amount > 500)
		)
		(up-compare-goal G-ABLE-TO-UPGRADE-ARCHERS >= HIGH)
	)
	(goal ALL-ECONOMY-TECH-DONE YES)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-leather-archer-armor)
; (chat-local-to-self "research LEATHER-ARCHER-ARMOR")
)

;***********************************************
;CHAIN-MAIL
;200 FOOD, 100 GOLD

(defrule
 	(can-research-with-escrow ri-chain-mail)
	(or
		(and
			(food-amount > 800)
			(gold-amount > 500)
		)
		(up-compare-goal G-ABLE-TO-UPGRADE-INFANTRY >= LOW)
	)
	(goal ALL-ECONOMY-TECH-DONE YES)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-chain-mail)
; (chat-local-to-self "research CHAIN-MAIL")
)

(defrule
 	(can-research-with-escrow ri-chain-mail)
	(goal RUSH-CONTROL RUSHING)
	(goal G-PRIMARY-UNIT eagle-warrior-line)
	(unit-type-count-total eagle-warrior-line > 3)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-chain-mail)
; (chat-local-to-self "research CHAIN-MAIL")
)
;*******************************************************************
;IMPERIAL-AGE



;***********************************************
;***********************************************
;BLAST-FURNACE
;275 FOOD, 225 GOLD

(defrule
 	(can-research-with-escrow ri-blast-furnace)
	(or
		(goal G-ABLE-TO-UPGRADE-INFANTRY HIGH)
		(goal G-ABLE-TO-UPGRADE-CAVALRY HIGH)
	)
	(goal ALL-ECONOMY-TECH-DONE YES)
	(goal G-UPGRADED-PRIMARY-UNIT YES)
	(goal G-UPGRADED-SECONDARY-UNIT YES)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-blast-furnace)
; (chat-local-to-self "research BLAST-FURNACE")
)

;***********************************************
;PLATE-MAIL
;300 FOOD, 150 GOLD

(defrule
 	(can-research-with-escrow ri-plate-mail)
	(or
		(and
			(food-amount > 800)
			(gold-amount > 500)
		)
		(up-compare-goal G-ABLE-TO-UPGRADE-INFANTRY >= MIDDLE)
	)
	(goal ALL-ECONOMY-TECH-DONE YES)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-plate-mail)
; (chat-local-to-self "research PLATE-MAIL")
)

;***********************************************
;PLATE-BARDING
;350 FOOD, 200 GOLD

(defrule
 	(can-research-with-escrow ri-plate-barding)
	(or
		(and
			(food-amount > 800)
			(gold-amount > 500)
		)
		(up-compare-goal G-ABLE-TO-UPGRADE-CAVALRY >= MIDDLE)
	)
	(goal ALL-ECONOMY-TECH-DONE YES)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-plate-barding)
; (chat-local-to-self "research PLATE-BARDING")
)

;***********************************************
;BRACER
;300 FOOD, 200 GOLD

(defrule
 	(can-research-with-escrow ri-bracer)
	(or
		(or
			(goal G-ABLE-TO-UPGRADE-ARCHERS MIDDLE)
			(building-type-count castle >= 2)
		)
		(unit-type-count-total galley-line >= FIVE-PERCENT-POP)
	)
	(goal ALL-ECONOMY-TECH-DONE YES)
=>
	(release-escrow food)
	(release-escrow gold)
 	(research ri-bracer)
; (chat-local-to-self "research BRACER")
)

;***********************************************
;RING-ARCHER-ARMOR
;250 FOOD, 250 GOLD

(defrule
 	(can-research-with-escrow ri-ring-archer-armor)
	(or
		(and
			(food-amount > 800)
			(gold-amount > 500)
		)
		(up-compare-goal G-ABLE-TO-UPGRADE-ARCHERS >= MIDDLE)
	)
	(goal ALL-ECONOMY-TECH-DONE YES)
	(goal G-UPGRADED-PRIMARY-UNIT YES)
	(goal G-UPGRADED-SECONDARY-UNIT YES)
=>
	(release-escrow food)
	(release-escrow gold)
	(research ri-ring-archer-armor)
; (chat-local-to-self "research RING-ARCHER-ARMOR")
)
