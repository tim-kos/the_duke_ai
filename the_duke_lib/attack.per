;========================================================================
;			ATTACK-CONTROLING (ATTACKER)

(defrule
	(true)
=>
	(set-goal ATTACK-PERSONALITY ATTACKER)
	(generate-random-number 2)
	(set-strategic-number sn-special-attack-type1 0)
	(set-strategic-number sn-special-attack-influence1 0)
	(set-strategic-number sn-percent-attack-soldiers 100)
	(set-strategic-number sn-percent-attack-boats 100)
	(disable-self)
)

;50/50 chance for defender/attacker personalities
(defrule
	(random-number > 1)
=>
	(set-goal ATTACK-PERSONALITY DEFENDER)
	(disable-self)
)

(defrule
	(current-age >= feudal-age)
	(town-under-attack)
	(up-compare-goal MILITARY-PARITY < PARITY)
	(timer-triggered T-NEED-HELP)
=>
	(disable-timer T-NEED-HELP)
	(chat-to-player every-ally "Need help!")
	(enable-timer T-NEED-HELP 150)
)

(defrule
	(current-age >= feudal-age)
	(taunt-detected any-human-ally 12)
	(town-under-attack)
=>
	(acknowledge-taunt this-any-human-ally 12)
	(chat-to-player this-any-human-ally "Cannot assist my allies at the moment, because my town is under attack!")
)

(defrule
	(current-age >= feudal-age)
	(taunt-detected any-human-ally 12)
	(military-population < 6)
=>
	(acknowledge-taunt this-any-human-ally 12)
	(chat-to-player this-any-human-ally "Cannot assist my allies at the moment, because I have only a small group of soldiers!")
)

(defrule
	(current-age >= feudal-age)
	(taunt-detected any-human-ally 12)
	(not (town-under-attack))
	(military-population >= 6)
=>
	(acknowledge-taunt this-any-human-ally 12)
	(set-strategic-number sn-target-evaluation-ally-proximity 10000)
	(set-strategic-number sn-percent-attack-soldiers 90)
	(attack-now)
	(chat-to-player this-any-human-ally "OK, I will help you!")
)
;------------------------------

#load-if-not-defined VICTORY-CONQUEST

(defrule
	(or
		(cc-players-building-type-count any-enemy wonder > 0)
		(cc-players-building-type-count any-neutral wonder > 0)
	)
=>
	(chat-to-player every-ally "39 Attack the enemy now! He's building a wonder!")
	(chat-to-player every-ally "26")
	(set-goal ATTACK-PERSONALITY ATTACKER)
	(disable-self)
)

(defrule
	(or
		(cc-players-building-type-count any-enemy wonder > 0)
		(cc-players-building-type-count any-neutral wonder > 0)
	)
=>
	(set-strategic-number sn-special-attack-type3 1)
	(set-strategic-number sn-percent-attack-soldiers 100)
)

(defrule
	(cc-players-building-type-count every-enemy wonder == 0)
	(cc-players-building-type-count every-neutral wonder == 0)
=>
	(set-strategic-number sn-special-attack-type3 0)
	(set-strategic-number sn-percent-attack-soldiers 100)
)
;-------------------------------------

(defrule
	(enemy-captured-relics)
=>
	(chat-to-player every-ally "39 Attack the enemy now! He has got all the relics!")
	(set-goal ATTACK-PERSONALITY ATTACKER)
	(disable-self)
)

#end-if		;if not victory-condition-conquest



#load-if-not-defined TEAMS-LOCKED

(defrule
	(cc-players-building-type-count any-neutral wonder > 0)
=>
	(chat-to-player this-any-neutral "41 You want to defeat me with a wonder? Before that, I will wipe you out!")
	(set-goal ATTACK-PERSONALITY ATTACKER)
	(set-stance this-any-neutral enemy)
)

#end-if		;if not teams-locked


(defrule
	(true)
=>
	(set-goal ATTACK-STATE READY)
	(set-goal ABLE-TO-ATTACK NO)
	(disable-self)
)

;31 == ATTACK AN ENEMY NOW taunt
;*****************************************
;*****************************************


(defrule
	(taunt-detected any-ally 31)
	(military-population < FIVE-PERCENT-POP)
=>
	(chat-to-player this-any-ally "39 Sorry, I don't attack! I have got only a few units and I need them to defend!")
	(acknowledge-taunt this-any-ally 31)
)

(defrule
	(taunt-detected any-ally 31)
	(goal ATTACK-STATE ATTACKING)
=>
	(chat-to-player this-any-ally "39 Sorry, I am attacking the enemy already.")
	(acknowledge-taunt this-any-ally 31)
)

(defrule
	(taunt-detected any-ally 31)
	(military-population >= FIVE-PERCENT-POP)
	(not (goal ATTACK-STATE ATTACKING))
=>
	(chat-local-to-self "Ok, I will prepare an attack to the enemy!")
	(chat-to-player this-any-ally "Ok, I will prepare an attack to the enemy!")
	(acknowledge-taunt this-any-ally 31)
	(set-goal ABLE-TO-ATTACK YES)
	(enable-timer T-ATTACK 30)
	(set-goal ATTACK-STATE PREPARING)
)

;RUSHING
;*****************************************
;*****************************************
(defrule
	(current-age == feudal-age)
	(goal RUSH-CONTROL RUSHING)
	(goal ABLE-TO-ATTACK YES)
	(goal ATTACK-STATE READY)
	(military-population >= 4)
	(players-building-count target-player > 0)
	(goal RUSH-ATTACK-STARTED NO)
	(up-compare-goal MILITARY-PARITY >= PARITY)
=>
	(enable-timer T-ATTACK 1);enabling the timer here means no retreat/regrouping before attack
	(set-goal ATTACK-STATE PREPARING)
	(chat-local-to-self "FLUSH")
	(chat-to-player every-ally "39 I do the Flush now!")
	(set-goal RUSH-ATTACK-STARTED YES)
	(disable-self)
)

(defrule
	(current-age == feudal-age)
	(goal RUSH-CONTROL RUSHING)
	(goal ABLE-TO-ATTACK YES)
	(goal ATTACK-STATE READY)
	(military-population >= 4)
	(goal MILITARY-PARITY VASTLY-SUPERIOR)
=>
	(disable-timer T-ATTACK)
	(enable-timer T-ATTACK 1);enabling the timer here means no retreat/regrouping before attack
	(set-goal ATTACK-STATE PREPARING)
)

(defrule
	(current-age == castle-age)
	(goal RUSH-CONTROL RUSHING)
	(goal ATTACK-STATE READY)
	(goal ABLE-TO-ATTACK YES)
	(or
		(and
			(military-population >= 6)
			(goal PRIMARY-COMBAT-UNIT KNIGHT)
		)
		(military-population >= 16)
	)
	(players-building-count target-player > 0)
	(up-compare-goal MILITARY-PARITY >= PARITY)

=>
	(enable-timer T-ATTACK 1);enabling the timer here means no retreat/regrouping before attack
	(set-goal ATTACK-STATE PREPARING)
	(chat-to-player every-ally "39 I rush the enemy now! Please protect my town, I have no units in there!")
	(set-goal RUSH-ATTACK-STARTED YES)
	(disable-self)
)

;REGULAR ATTACKS
;*****************************************
;*****************************************

(defrule
	(up-timer-status T-ATTACK != timer-running)
	(goal ATTACK-STATE READY)
	(goal ABLE-TO-ATTACK YES)
	(population-headroom < 10)
	(current-age >= castle-age)
=>
	(set-goal ATTACK-STATE PREPARING)
	(chat-local-to-self "Prepare attack1")
)

(defrule
	(up-timer-status T-ATTACK != timer-running)
	(goal ATTACK-STATE READY)
	(goal ABLE-TO-ATTACK YES)
	(or
		(enemy-captured-relics)
		(cc-players-building-type-count any-enemy wonder > 0)
	)
	(defend-soldier-count >= EIGHT-PERCENT-POP)
	(current-age >= castle-age)
=>
	(set-goal ATTACK-STATE PREPARING)
	(set-strategic-number sn-special-attack-type1 1)
	(set-strategic-number sn-special-attack-type3 -1)
	(chat-local-to-self "Prepare attack2")
)

(defrule
	(up-timer-status T-ATTACK != timer-running)
	(goal ATTACK-STATE READY)
	(goal ABLE-TO-ATTACK YES)
	(cc-players-building-type-count any-enemy wonder > 0)
	(defend-soldier-count >= EIGHT-PERCENT-POP)
	(current-age >= castle-age)
=>
	(set-goal ATTACK-STATE PREPARING)
	(set-strategic-number sn-special-attack-type1 -1)
	(set-strategic-number sn-special-attack-type3 1)
	(chat-local-to-self "Prepare attack3")
)

(defrule
	(up-timer-status T-ATTACK != timer-running)
	(goal ATTACK-STATE READY)
	(goal ABLE-TO-ATTACK YES)
	(defend-soldier-count >= THIRTY-PERCENT-POP)
	(or
		(unit-type-count trebuchet > 0)
		(unit-type-count battering-ram-line > 0)
	)
	(current-age >= castle-age)
	(up-compare-goal MILITARY-PARITY >= PARITY)
	(goal SAVE-FOR-RESEARCH NO)
	(goal RUSH-CONTROL NO)
=>
	(set-goal ATTACK-STATE PREPARING)
	(chat-local-to-self "Prepare attack4")
)

;If we are an aggressive personality, attack based on current-age-time
(defrule
	(up-timer-status T-ATTACK != timer-running)
	(goal ATTACK-STATE READY)
	(goal ABLE-TO-ATTACK YES)
	(or
		(and
			(current-age-time > 60)
			(death-match-game)
		)
		(current-age-time > 420)
	)
	(current-age >= castle-age)
	(goal RUSH-CONTROL NO)
	(goal SAVE-FOR-RESEARCH NO)
	(goal ATTACK-PERSONALITY ATTACKER)
	(up-compare-goal MILITARY-PARITY > INFERIOR)
=>
	(set-goal ATTACK-STATE PREPARING)
	(chat-local-to-self "Prepare attack4")
)


(defrule
	(up-timer-status T-ATTACK != timer-running)
	(goal ABLE-TO-ATTACK YES)
	(or
		(and
			(goal MILITARY-PARITY VASTLY-SUPERIOR)
			(defend-soldier-count >= TWENTY-PERCENT-POP)
		)
		(and
			(goal MILITARY-PARITY SUPERIOR)
			(soldier-count >= THIRTY-PERCENT-POP)
		)
	)
	(goal SAVE-FOR-RESEARCH NO)
=>
	(set-goal ATTACK-STATE PREPARING)
	(chat-local-to-self "Prepare attack5")
)

;------------------------


;If our attack timer window is up we will stop the attack.
(defrule
	(timer-triggered T-ATTACK)
	(goal ATTACK-STATE ATTACKING)
=>
	(disable-timer T-ATTACK)
	(set-goal ATTACK-STATE STOP)
	(chat-local-to-self "STOP THE ATTACK")
)

(defrule
	(goal ATTACK-STATE ATTACKING)
	(up-compare-goal MILITARY-PARITY < PARITY)
=>
	(disable-timer T-ATTACK)
	(set-goal ATTACK-STATE STOP)
	(up-retreat-now)
	(chat-local-to-self "RETREAT")
	(chat-to-player every-ally "I will retreat my soldiers from the battlefield.")
)

(defrule
	(goal ATTACK-STATE STOP)
=>
	(set-goal ATTACK-STATE RESET-TOWN-SIZE)
	(set-strategic-number sn-enemy-sighted-response-distance enemy-sighted-response-distance)
	(chat-local-to-self "RESET TOWN SIZE")
	(chat-to-player every-ally "I will stop sending any additional units into battle for now.")
)

(defrule
	(goal ATTACK-STATE TOWN-SIZE-RESETTED)
	(goal ATTACK-PERSONALITY ATTACKER)
=>
	(set-goal ATTACK-STATE READY)
	(enable-timer T-ATTACK TIME-BETWEEN-ATTACK-ATTEMPTS-ATTACKER)
	(chat-local-to-self "TOWN SIZE RESETTED")
)

(defrule
	(goal ATTACK-STATE TOWN-SIZE-RESETTED)
	(goal ATTACK-PERSONALITY DEFENDER)
=>
	(set-goal ATTACK-STATE READY)
	(enable-timer T-ATTACK TIME-BETWEEN-ATTACK-ATTEMPTS-DEFENDER)
	(chat-local-to-self "TOWN SIZE RESETTED2")
)

;---------------------------------------------



(defrule
	(goal RUSH-CONTROL RUSHING)
	(can-build watch-tower)
	(building-type-count-total watch-tower < 3)
	(goal ATTACK-PERSONALITY ATTACKER)
	(goal RUSH-ATTACK-STARTED YES)
	(current-age == feudal-age)
=>
	(build-forward watch-tower)
)
;-----------------------------------


(defrule
	(or
		(and
			(game-time > GAME-TIME-UNTIL-FIRST-ATTACK-WHEN-DEFENDER-PERSONALITY)
			(goal ATTACK-PERSONALITY DEFENDER)
		)
		(and
			(game-time > GAME-TIME-UNTIL-FIRST-ATTACK-WHEN-ATTACKER-PERSONALITY)
			(goal ATTACK-PERSONALITY ATTACKER)
		)
	)
	(goal ABLE-TO-ATTACK NO)
=>
	(set-goal ABLE-TO-ATTACK YES)
	(chat-local-to-self "SET ABLE TO ATTACK YES")
)

(defrule
	(or
		(and
			(not (player-in-game any-neutral))
			(not (player-in-game any-enemy))
		)
		(building-type-count-total wonder > 0)
	)
	(goal ABLE-TO-ATTACK YES)
=>
	(set-goal ABLE-TO-ATTACK NO)
	(chat-local-to-self "SET ABLE TO ATTACK NO")
)



;*********************************


(defrule
	(goal ABLE-TO-ATTACK NO)
=>
	(set-strategic-number sn-percent-attack-soldiers 0)
	(set-strategic-number sn-number-attack-groups 0)
)
;----------------------------------


(defrule
	(up-timer-status T-ATTACK != timer-running)
	(goal ATTACK-STATE PREPARING)
=>
	(set-goal ATTACK-STATE PULLING-TROOPS)
	;(up-retreat-now) ;Pull all units together at the town center, so when the attack starts, they all move as one blob and not scattered groups
	(set-strategic-number sn-task-ungrouped-soldiers YES);spread units a bit
	(chat-local-to-self "preparing for attack!")
	;(chat-to-player every-ally "39 I will attack the enemy in one minute!")
	(enable-timer T-ATTACK 10)
	(chat-local-to-self "PULL TROOPS TOGETHER")
)

(defrule
	(goal ATTACK-STATE PULLING-TROOPS)
	(timer-triggered T-ATTACK)
	(goal CAN-USE-TOWN-SIZE-ATTACK YES)
=>
	(set-goal ATTACK-STATE ATTACKING)
	(set-strategic-number sn-percent-attack-soldiers 100)
	(set-strategic-number sn-percent-attack-boats 100)
	(set-strategic-number sn-task-ungrouped-soldiers NO)
	(enable-timer T-ATTACK 180)
	(set-strategic-number sn-enemy-sighted-response-distance 10)
	(set-strategic-number sn-maximum-town-size 450)
	(chat-to-player every-ally "I send new troops into battle! Hopefully you can reinforce them!")
	(chat-local-to-self "ATTACK NOW!")
)

(defrule
	(goal ATTACK-STATE PULLING-TROOPS)
	(timer-triggered T-ATTACK)
	(goal CAN-USE-TOWN-SIZE-ATTACK NO)
=>
	(set-goal ATTACK-STATE ATTACKING)
	(enable-timer T-ATTACK 180)
	(set-strategic-number sn-percent-attack-soldiers 90)
	(set-strategic-number sn-percent-attack-boats 90)
	(set-strategic-number sn-task-ungrouped-soldiers NO)
	(attack-now)
	(chat-local-to-self "ATTACK NOW!")
	(chat-to-player every-ally "I send new troops into battle! Hopefully you can reinforce my units!")
)


(defrule
	(military-population >= FIVE-PERCENT-POP)
	(cc-players-unit-count every-enemy < 5)
	(cc-players-building-type-count every-enemy castle < 2)
=>
	(set-strategic-number sn-percent-attack-soldiers 100)
	(set-strategic-number sn-percent-attack-boats 100)
	(attack-now)
)



;TAUNTS 48 and 49 (be more aggressive and be more defensive)
;************************************************************************************************************



;Taunt 48 == BE MORE AGGRESSIVE!
;*****************************************
;*****************************************

(defrule
	(or
		(or
			(taunt-detected any-ally 48)
			(taunt-detected any-neutral 48)
		)
		(taunt-detected any-enemy 48)
	)
=>
	(set-goal ATTACK-PERSONALITY ATTACKER)
	(acknowledge-taunt this-any-ally 48)
	(acknowledge-taunt this-any-neutral 48)
	(acknowledge-taunt this-any-enemy 48)
	(chat-to-player this-any-ally "Okay, I will be more aggressive!")
	(chat-to-player this-any-neutral "Okay, I will be more aggressive!")
	(chat-to-player this-any-enemy "Okay, I will be more aggressive!")
)
;*****************************************
;*****************************************

(defrule
	(or
		(or
			(taunt-detected any-ally 49)
			(taunt-detected any-neutral 49)
		)
		(taunt-detected any-enemy 49)
	)
=>
	(set-goal ATTACK-PERSONALITY DEFENDER)
	(acknowledge-taunt this-any-ally 49)
	(acknowledge-taunt this-any-neutral 49)
	(acknowledge-taunt this-any-enemy 49)
	(chat-to-player this-any-ally "Okay, I will be less aggressive!")
	(chat-to-player this-any-neutral "Okay, I will be less aggressive!")
	(chat-to-player this-any-enemy "Okay, I will be less aggressive!")
)
