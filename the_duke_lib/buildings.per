;************************************************************************************************************
;************************************************************************************************************
;STUFF FOR THE TOWN-SIZE

;Rule1: set the town size in the dark age

(defrule
	(current-age == dark-age)
	(not (civ-selected hun))
=>
	(set-strategic-number sn-minimum-town-size 6)
	(set-strategic-number sn-maximum-town-size 10)
	(disable-self)
)

(defrule
	(current-age == dark-age)
	(civ-selected hun)
=>
	(set-strategic-number sn-minimum-town-size 6)
	(set-strategic-number sn-maximum-town-size 8)
	(disable-self)
)

(defrule
	(current-age == feudal-age)
	(not (civ-selected hun))
=>
	(set-strategic-number sn-minimum-town-size 6)
	(set-strategic-number sn-maximum-town-size 12)
	(disable-self)
)

(defrule
	(current-age == feudal-age)
	(civ-selected hun)
=>
	(set-strategic-number sn-minimum-town-size 6)
	(set-strategic-number sn-maximum-town-size 10)
	(disable-self)
)

(defrule
	(current-age == castle-age)
	(not (civ-selected hun))
	(goal RUSH-CONTROL RUSHING)
=>
	(set-strategic-number sn-minimum-town-size 12)
	(set-strategic-number sn-maximum-town-size 18)
	(disable-self)
)

(defrule
	(current-age == castle-age)
	(civ-selected hun)
	(goal RUSH-CONTROL RUSHING)
=>
	(set-strategic-number sn-minimum-town-size 10)
	(set-strategic-number sn-maximum-town-size 16)
	(disable-self)
)

(defrule
	(current-age == castle-age)
	(not (civ-selected hun))
	(goal RUSH-CONTROL NO)
=>
	(set-strategic-number sn-minimum-town-size 14)
	(set-strategic-number sn-maximum-town-size 23)
	(disable-self)
)

(defrule
	(current-age == castle-age)
	(civ-selected hun)
	(goal RUSH-CONTROL NO)
=>
	(set-strategic-number sn-minimum-town-size 12)
	(set-strategic-number sn-maximum-town-size 20)
	(disable-self)
)

(defrule
	(game-time > 2400)
=>
	(set-strategic-number sn-minimum-town-size min-imperial-town-size)
	(set-strategic-number sn-maximum-town-size max-imperial-town-size)
	(disable-self)
)
;************************************************************************************************************
;HOUSING



#load-if-not-defined DEATH-MATCH
;***********************************************
;***********************************************

;Rule6: build HOUSES - in games which aren't deathmatch games

(defrule
	(or
		(housing-headroom <= 2)
		(and
			(game-time >= 300)
			(housing-headroom <= 4)
		)
	)
 	(can-build house)
	(population-headroom != 0)
=>
	(build house)
;	(chat-local-to-self "build HOUSE")
)

(defrule
	(or
		(housing-headroom <= 2)
		(and
			(game-time >= 300)
			(housing-headroom <= 8)
		)
	)
 	(can-build house)
	(population-headroom != 0)
	(current-age > dark-age)
	(or
		(goal RUSH-CONTROL CASTLE)
		(goal RUSH-CONTROL RUSHING)
	)
=>
	(build house)
;	(chat-local-to-self "build HOUSE")
)

(defrule
	(game-time >= 900)
	(current-age-time > 120)
	(housing-headroom <= 16)
 	(can-build house)
	(population-headroom != 0)
	(current-age >= castle-age)
=>
	(build house)
;	(chat-local-to-self "build HOUSE")
)
;***********************************************
#else		;if not game-type == death-match
;***********************************************
;***********************************************

;Rule7: build HOUSES - in deathmatch games

(defrule
	(or
		(and
			(difficulty >= easy)
			(housing-headroom <= 6)
		)
		(and
			(difficulty <= moderate)
			(housing-headroom <= 20)
		)
	)
 	(can-build house)
	(population-headroom != 0)
=>
	(build house)
;	(chat-local-to-self "build HOUSE")
)
;***********************************************
#end-if		;if game-type == death-match



;REBUILD TOWN-CENTER
;-------------------------------------

(defrule
 	(building-type-count-total town-center == 0)
 	(can-build town-center)
	(not (map-type nomad))
=>
 	(build town-center)
;	(chat-local-to-self "rebuild TOWN-CENTER")
)


;BUILD TOWN-CENTER IN NOMAD-MAP
;----------------------------------------------------------



;Rule8: build TOWN-CENTER

(defrule
	(map-type nomad)
  (game-time > 30)
	(building-type-count town-center < 1)
	(can-build town-center)
	(resource-found wood)
=>
 	(build town-center)
;	(chat-local-to-self "rebuild TOWN-CENTER")
)
;************************************************************************************************************
;LUMBER CAMPS



#load-if-not-defined DEATH-MATCH
;***********************************************
;***********************************************


;Rule10: build LUMBER-CAMP first one when we found some wood in wonder race games

(defrule
 	(resource-found wood)
	(can-build lumber-camp)
	(building-type-count-total lumber-camp < 1)
	(building-type-count-total mill > 0)
	(or
		(civ-selected hun)
		(building-type-count house > 0)
	)
	(unit-type-count-total villager > 10)
=>
 	(build lumber-camp)
;	(chat-local-to-self "build LUMBER-CAMP")
)
;***********************************************
#else		;if not game-type == death-match
;***********************************************
;***********************************************

;Rule9: build LUMBER-CAMP first one when we found some wood in games which aren't wonder race games

(defrule
	(can-build lumber-camp)
	(building-type-count-total lumber-camp < 1)
	(building-type-count-total town-center > 0)
	(or
		(civ-selected hun)
		(building-type-count house > 0)
	)
=>
 	(build lumber-camp)
;	(chat-local-to-self "build LUMBER-CAMP")
)
;***********************************************
#end-if		;if game-type == death match



;*******************************************************************
;*******************************************************************
;EXTRA LUMBER-CAMPS // The villagers won't get the ood from only one lumber camp

;Rule15: build EXTRA-LUMBERCAMP if the wood is too far away

(defrule
	(current-age >= feudal-age)
	(strategic-number sn-wood-gatherer-percentage > 0)
	(can-build lumber-camp)
	(resource-found wood)
	(or
		(and
			(dropsite-min-distance wood > 3)
			(and
				(building-type-count-total town-center > 1)
				(not (research-available ri-hand-cart))
			)
		)
		(dropsite-min-distance wood > 4)
	)
	(dropsite-min-distance wood < 480)
	(building-type-count-total barracks > 0)
	(building-type-count-total blacksmith > 0)
	(up-pending-objects c: lumber-camp <= 1)
=>
	(build lumber-camp)
;	(chat-local-to-self "build LUMBER-CAMP")
)

;************************************************************************************************************
;GOLD- AND STONE-MINING



#load-if-defined WONDER-RACE
;***********************************************
;***********************************************

;Rule16: build MINING-CAMP GOLD in dark-age and we found some gold (it mustn't be so far from the next camp) and we play wonder race

(defrule
 	(resource-found gold)
	(dropsite-min-distance gold > 4)
 	(can-build mining-camp)
	(building-type-count-total mining-camp < 1)
	(building-type-count-total lumber-camp > 0)
	(or
		(current-age >= feudal-age)
		(goal ADVANCING-TO-AGE FEUDAL-AGE)
	)
=>
 	(build mining-camp)
;	(chat-local-to-self "build MINING-CAMP for GOLD")
)

(defrule
	(resource-found stone)
	(dropsite-min-distance stone > 4)
 	(can-build mining-camp)
	(building-type-count-total mining-camp == 1)
	(building-type-count-total lumber-camp > 0)
	(or
		(current-age >= feudal-age)
		(goal ADVANCING-TO-AGE FEUDAL-AGE)
	)
=>
 	(build mining-camp)
;	(chat-local-to-self "build MINING-CAMP for STONE")
)

(defrule
 	(current-age == imperial-age)
 	(resource-found gold)
	(dropsite-min-distance gold > 4)
 	(can-build mining-camp)
	(building-type-count-total mining-camp < 6)
	(building-type-count-total lumber-camp > 0)
=>
 	(build mining-camp)
;	(chat-local-to-self "build MINING-CAMP for GOLD")
)

(defrule
 	(current-age == imperial-age)
	(resource-found stone)
	(dropsite-min-distance stone > 4)
 	(can-build mining-camp)
	(building-type-count-total mining-camp < 6)
	(building-type-count-total lumber-camp > 0)
	(building-type-count-total market > 0)
=>
 	(build mining-camp)
;	(chat-local-to-self "build MINING-CAMP for STONE")
)
;***********************************************
#end-if		;if game type == wonder race



#load-if-not-defined WONDER-RACE
#load-if-not-defined DEATH-MATCH
;***********************************************
;***********************************************

;Rule20: build MINING-CAMPS through the ages

(defrule
 	(or
		(current-age >= feudal-age)
		(goal ADVANCING-TO-AGE FEUDAL-AGE)
	)
	(resource-found gold)
	(dropsite-min-distance gold > 4)
 	(can-build mining-camp)
	(building-type-count-total lumber-camp > 0)
=>
 	(build mining-camp)
;	(chat-local-to-self "build MINING-CAMP")
)

(defrule
	(current-age >= feudal-age)
	(or
		(goal RUSH-CONTROL NO)
		(or
			(building-type-count-total town-center > 1)
			(stone-amount < 100)
		)
	)
	(resource-found stone)
	(dropsite-min-distance stone > 4)
 	(can-build mining-camp)
	(building-type-count-total blacksmith > 0)
	(building-type-count-total barracks > 0)
	(building-type-count-total lumber-camp > 0)
	(building-type-count-total mining-camp > 0)
=>
 	(build mining-camp)
;	(chat-local-to-self "build MINING-CAMP")
)

;***********************************************
#else		;if not game-type == death-match
;***********************************************
;***********************************************
;Rule21: build MINING-CAMPS THROUGH THE OTHER AGES

(defrule
	(or
		(and
			(resource-found gold)
			(dropsite-min-distance gold > 4)
		)
		(and
			(resource-found stone)
			(dropsite-min-distance stone > 4)
		)
	)
 	(can-build mining-camp)
	(building-type-count-total mining-camp < 20)
	(building-type-count-total lumber-camp > 0)
	(current-age >= feudal-age)
=>
 	(build mining-camp)
;	(chat-local-to-self "build MINING-CAMP")
)
;***********************************************
#end-if		;if game-type == death-match
#end-if		;if not game-type == wonder-race


;************************************************************************************************************
;************************************************************************************************************
;MILLS


;build MILL (FIRST) when we found some berry-bush, deer or fish
(defrule
	(or
		(resource-found food)
		(current-age-time > 250)
	)
	(can-build mill)
	(building-type-count-total mill < 1)
=>
 	(build mill)
;	(chat-local-to-self "build MILL")
)


;extra mill if we found deer
(defrule
	(dropsite-min-distance food > 8)
	(dropsite-min-distance food < 25)
	(building-type-count-total lumber-camp > 0)
	(building-type-count-total mill < 2)
	(resource-found food)
	(can-build mill)
=>
	(build mill)
;	(chat-local-to-self "build MILL")
)


#load-if-not-defined WONDER-RACE
;***********************************************
;***********************************************



#load-if-not-defined YUCATAN-MAP

;Rule23: build EXTRA MILLS to collect gather the last berry-bushes, deers, boars and fishes on the map to save wood (we could use the wood to build farms)

(defrule
	(resource-found food)
	(can-build mill)
	(or
		(unit-type-count 122 > 0)
		(unit-type-count 216 > 0)
	)
	(building-type-count-total mill == 1)
	(unit-type-count 354 == 0)
	(unit-type-count 120 == 0)
	(wood-amount > 300)
	(building-type-count-total town-center > 2)
	(building-type-count-total monastery > 0)
	(building-type-count-total blacksmith > 0)
=>
 	(build mill)
;	(chat-local-to-self "build MILL")
)

(defrule
	(resource-found food)
	(can-build mill)
	(or
		(unit-type-count 56 > 0)
		(unit-type-count 57 > 0)
	)
	(building-type-count-total mill == 2)
	(unit-type-count 354 == 0)
	(unit-type-count 120 == 0)
	(wood-amount > 300)
	(building-type-count-total town-center > 2)
	(building-type-count-total monastery > 0)
	(building-type-count-total blacksmith > 0)
=>
 	(build mill)
;	(chat-local-to-self "build MILL")
)
;***********************************************
#else			;if we do not play on the yucatan map
;***********************************************
;***********************************************

;Rule23: build EXTRA MILLS to collect gather the last berry-bushes, deers, boars and fishes on the map to save wood (we could use the wood to build farms)

(defrule
	(resource-found food)
	(can-build mill)
	(building-type-count-total mill == 1)
	(or
		(unit-type-count 354 > 0)
		(unit-type-count 120 > 0)
	)
	(wood-amount > 200)
	(building-type-count-total town-center > 1)
	(building-type-count-total monastery > 0)
	(building-type-count-total blacksmith > 0)
=>
 	(build mill)
;	(chat-local-to-self "build MILL")
)

(defrule
	(resource-found food)
	(can-build mill)
	(or
		(unit-type-count 122 > 0)
		(unit-type-count 216 > 0)
	)
	(building-type-count-total mill == 2)
	(unit-type-count 354 == 0)
	(unit-type-count 120 == 0)
	(wood-amount > 200)
	(building-type-count-total town-center > 2)
	(building-type-count-total monastery > 0)
	(building-type-count-total blacksmith > 0)
=>
 	(build mill)
;	(chat-local-to-self "build MILL")
)

(defrule
	(resource-found food)
	(can-build mill)
	(or
		(unit-type-count 56 > 0)
		(unit-type-count 57 > 0)
	)
	(building-type-count-total mill == 3)
	(unit-type-count 354 == 0)
	(unit-type-count 120 == 0)
	(wood-amount > 200)
	(building-type-count-total town-center > 2)
	(building-type-count-total monastery > 0)
	(building-type-count-total blacksmith > 0)
=>
 	(build mill)
;	(chat-local-to-self "build MILL")
)
;***********************************************
#end-if		;if we play on the yucatan map

;Rule23: build EXTRA MILLS to collect gather the last berry-bushes, deers, boars and fishes on the map to save wood (we could use the wood to build farms)

(defrule
 	(current-age >= castle-age)
	(resource-found food)
	(dropsite-min-distance food >= 4)
	(can-build mill)
	(building-type-count-total mill < 5)
	(wood-amount > 300)
	(building-type-count-total town-center > 2)
	(building-type-count-total monastery > 0)
	(building-type-count-total blacksmith > 0)
=>
 	(build mill)
;	(chat-local-to-self "build MILL")
)
;***********************************************
#end-if		;if game-type == wonder race



;Rule24: build EXTRA MILLS when we have got many farms
(defrule
	(building-type-count farm > 15)
	(building-type-count lumber-camp > 0)
	(building-type-count-total mill < 2)
	(can-build mill)
	(building-type-count-total town-center > 1)
=>
	(build mill)
)

(defrule
	(or
		(or
			(and
				(building-type-count-total farm > 25)
				(building-type-count-total mill < 3)
			)
			(and
				(building-type-count-total farm > 35)
				(building-type-count-total mill < 4)
			)
		)
		(and
			(building-type-count-total farm > 45)
			(building-type-count-total mill < 5)
		)
	)
	(can-build mill)
	(building-type-count lumber-camp > 0)
	(building-type-count-total town-center > 2)
=>
	(build mill)
;	(chat-local-to-self "build MILL (many farms)")
)
;---------------



#load-if-not-defined WONDER-RACE


;Rule31: build FARMS-dark-age when we haven't got sheeps or turkeys anymore

(defrule
 	(current-age == dark-age)
 	(can-build farm)
	(building-type-count-total farm < 9)
	(or
		(and
			(unit-type-count SHEEP < 2)
			(unit-type-count TURKEY < 2)
		)
		(current-age-time > 360)
	)
	(game-time > 300)
	(building-type-count-total lumber-camp > 0)
	(idle-farm-count < 1)
=>
 	(build farm)
;	(chat-local-to-self "build FARM")
)

(defrule
 	(current-age == dark-age)
 	(can-build farm)
	(building-type-count-total farm < 14)
	(sheep-and-forage-too-far)
	(game-time > 300)
	(building-type-count-total lumber-camp > 0)
	(idle-farm-count < 2)
=>
 	(build farm)
;	(chat-local-to-self "build FARM")
)

(defrule
 	(current-age == feudal-age)
 	(can-build farm)
 	(or
		(building-type-count-total farm < 9)
		(and
			(wood-amount > 100)
			(building-type-count-total farm < 14)
		)
	)
	(sheep-and-forage-too-far)
	(building-type-count-total lumber-camp > 0)
	(nand
		(goal RUSH-CONTROL CASTLE)
		(goal PRIMARY-COMBAT-UNIT EAGLEMAN)
	)
	(idle-farm-count < 2)
=>
 	(build farm)
;	(chat-local-to-self "build FARM")
)

(defrule
 	(current-age == feudal-age)
 	(can-build farm)
	(sheep-and-forage-too-far)
	(building-type-count-total farm <= max-feudal-farm)
	(or
		(building-type-count-total market > 0)
		(or
			(building-type-count-total archery-range > 0)
			(building-type-count-total stable > 0)
		)
	)
	(building-type-count-total blacksmith > 0)
	(nand
		(goal RUSH-CONTROL CASTLE)
		(goal PRIMARY-COMBAT-UNIT EAGLEMAN)
	)
	(building-type-count-total lumber-camp > 0)
	(idle-farm-count < 1)
	(wood-amount > 150)
=>
 	(build farm)
;	(chat-local-to-self "build FARM")
)

(defrule
 	(current-age == feudal-age)
 	(can-build farm)
	(sheep-and-forage-too-far)
	(building-type-count-total farm <= 8)
	(or
		(building-type-count-total market > 0)
		(building-type-count-total archery-range > 0)
	)
	(building-type-count-total blacksmith > 0)
	(goal RUSH-CONTROL CASTLE)
	(goal PRIMARY-COMBAT-UNIT EAGLEMAN)
	(idle-farm-count < 2)
	(building-type-count-total lumber-camp > 0)
=>
 	(build farm)
;	(chat-local-to-self "build FARM")
)

(defrule
 	(current-age == feudal-age)
 	(can-build farm)
	(sheep-and-forage-too-far)
	(building-type-count-total barracks >= 4)
	(building-type-count-total farm <= max-feudal-farm)
	(or
		(building-type-count-total market > 0)
		(building-type-count-total archery-range > 0)
	)
	(building-type-count-total blacksmith > 0)
	(goal RUSH-CONTROL CASTLE)
	(goal PRIMARY-COMBAT-UNIT EAGLEMAN)
	(idle-farm-count < 2)
	(building-type-count-total lumber-camp > 0)
=>
 	(build farm)
;	(chat-local-to-self "build FARM")
)

(defrule
 	(current-age == feudal-age)
 	(can-build farm)
	(sheep-and-forage-too-far)
	(building-type-count-total farm <= 8)
	(building-type-count-total stable > 0)
	(building-type-count-total blacksmith > 0)
	(goal RUSH-CONTROL CASTLE)
	(goal PRIMARY-COMBAT-UNIT EAGLEMAN)
	(idle-farm-count == 0)
	(building-type-count-total lumber-camp > 0)
=>
 	(build farm)
;	(chat-local-to-self "build FARM")
)

(defrule
 	(current-age == feudal-age)
 	(can-build farm)
	(sheep-and-forage-too-far)
	(building-type-count-total barracks >= 4)
	(building-type-count-total farm <= max-feudal-farm)
	(building-type-count-total stable > 0)
	(building-type-count-total blacksmith > 0)
	(goal RUSH-CONTROL CASTLE)
	(goal PRIMARY-COMBAT-UNIT EAGLEMAN)
	(idle-farm-count == 0)
	(building-type-count-total lumber-camp > 0)
=>
 	(build farm)
;	(chat-local-to-self "build FARM")
)

(defrule
 	(current-age >= castle-age)
 	(can-build farm)
 	(sheep-and-forage-too-far)
	(building-type-count-total farm < 15)
	(building-type-count-total lumber-camp > 0)
	(idle-farm-count == 0)
=>
 	(build farm)
;	(chat-local-to-self "build FARM")
)

(defrule
 	(can-build farm)
 	(current-age == castle-age)
	(building-type-count-total farm <= max-castle-farm)
	(or
		(building-type-count-total archery-range > 0)
		(or
			(building-type-count-total stable > 0)
			(building-type-count-total castle > 0)
		)
	)
	(building-type-count-total blacksmith > 0)
	(building-type-count-total town-center > 1)
	(not (research-available ri-hand-cart))
	(building-type-count-total lumber-camp > 0)
	(idle-farm-count < 3)
=>
 	(build farm)
;	(chat-local-to-self "build FARM")
)

(defrule
 	(can-build farm)
	(current-age == imperial-age)
	(building-type-count-total farm <= max-imperial-farm)
	(or
		(building-type-count-total archery-range > 0)
		(or
			(building-type-count-total stable > 0)
			(building-type-count-total castle > 0)
		)
	)
	(building-type-count-total blacksmith > 0)
	(building-type-count-total town-center > 1)
	(not (research-available ri-hand-cart))
	(building-type-count-total lumber-camp > 0)
	(idle-farm-count < 3)
=>
 	(build farm)
;	(chat-local-to-self "build FARM")
)

;***********************************************
#else		;if not game type == wonder race
;***********************************************
;***********************************************



;Rule31: build FARMS-dark-age when we haven't got sheeps or turkeys anymore

(defrule
 	(current-age == dark-age)
 	(can-build farm)
	(building-type-count-total farm <= max-dark-farm)
	(or
		(and
			(unit-type-count SHEEP < 2)
			(unit-type-count TURKEY < 2)
		)
		(current-age-time > 360)
	)
	(game-time > 300)
	(building-type-count-total lumber-camp > 0)
=>
 	(build farm)
;	(chat-local-to-self "build FARM")
)

(defrule
 	(current-age == feudal-age)
 	(can-build farm)
	(building-type-count-total farm < 8)
	(or
		(and
			(unit-type-count SHEEP < 2)
			(unit-type-count TURKEY < 2)
		)
		(current-age-time > 360)
	)
	(building-type-count-total lumber-camp > 0)
=>
 	(build farm)
;	(chat-local-to-self "build FARM")
)

(defrule
 	(current-age == feudal-age)
 	(can-build farm)
	(or
		(sheep-and-forage-too-far)
		(current-age-time > 600)
	)
	(building-type-count-total farm <= max-feudal-farm)
	(or
		(or
			(building-type-count-total market > 0)
			(building-type-count-total archery-range > 0)
		)
		(building-type-count-total stable > 0)
	)
	(building-type-count-total lumber-camp > 0)
=>
 	(build farm)
;	(chat-local-to-self "build FARM")
)

(defrule
 	(current-age >= castle-age)
 	(can-build farm)
	(building-type-count-total farm < 15)
	(or
		(and
			(unit-type-count SHEEP < 2)
			(unit-type-count TURKEY < 2)
		)
		(current-age-time > 360)
	)
	(building-type-count-total lumber-camp > 0)
=>
 	(build farm)
;	(chat-local-to-self "build FARM")
)


#end-if		;if game type == wonder race
;***********************************************
;***********************************************




;Rule42: build extra farms

(defrule
	(current-age == imperial-age)
	(building-type-count-total farm >= max-imperial-farm)
	(idle-farm-count < 2)
	(can-build farm)
	(building-type-count-total lumber-camp > 0)
=>
	(build farm)
;	(chat-local-to-self "build extra farm")
)
;************************************************************************************************************
;CIVIL BUILDINGS



#load-if-not-defined DEFEND-WONDER
;***********************************************
;***********************************************

#load-if-not-defined WONDER-RACE
;***********************************************
;***********************************************

#load-if-not-defined DEATH-MATCH
;***********************************************
;***********************************************

;Rule43: build BLACKSMITH - to build one of the required buildings to research to the castle-age in random map- or defend-the-wonder - games (or king of the hill)

(defrule
	(building-type-count-total blacksmith < 1)
 	(current-age >= feudal-age)
 	(can-build blacksmith)
	(or
		(building-type-count-total mining-camp > 0)
		(gold-amount >= 200)
	)
	(or
		(not (goal RUSH-CONTROL RUSHING))
		(or
			(building-type-count-total archery-range > 0)
			(building-type-count-total stable > 0)
		)
	)
	(or
		(and
			(not (research-available ri-wheel-barrow))
			(and
				(not (research-available ri-double-bit-axe))
				(not (research-available ri-horse-collar))
			)
		)
		(wood-amount > 500)
	)
=>
 	(build blacksmith)
;	(chat-local-to-self "build BLACKSMITH")
)
;***********************************************
#else		;if not game-type == death-match
;***********************************************
;***********************************************

;Rule44: build BLACKSMITH - to build one of the required buildings to research to the castle-age in death-match games

(defrule
	(building-type-count-total blacksmith < 2)
 	(current-age >= feudal-age)
 	(can-build blacksmith)
	(not (current-age == post-imperial-age))
=>
 	(build blacksmith)
;	(chat-local-to-self "build BLACKSMITH")
)
;***********************************************
#end-if		;if game-type == death-match

;***********************************************
;***********************************************
#else		;if not game-type == wonder-race
;***********************************************
;***********************************************

;Rule45: build MARKET in imperial age with escrow-resources

(defrule
 	(building-type-count-total market < 1)
 	(can-build market)
=>				 ;then
 	(build market)
;	(chat-local-to-self "build MARKET")
)

(defrule
	(building-type-count-total blacksmith < 1)
 	(current-age >= feudal-age)
 	(can-build blacksmith)
	(building-type-count-total market > 0)
	(not (current-age == post-imperial-age))
=>
 	(build blacksmith)
;	(chat-local-to-self "build BLACKSMITH")
)
;***********************************************
#end-if		;if game-type == wonder race

;***********************************************
;***********************************************
#end-if		;if not game-type == defend-wonder


;Rule47: build MARKET- to build the other required building to research to the castle-age

(defrule
 	(building-type-count-total market < 1)
 	(can-build market)
	(current-age == feudal-age)
	(goal RUSH-CONTROL RUSHING)
	(building-type-count-total stable == 0)
	(building-type-count-total archery-range == 0)
	(goal ABLE-TO-RESEARCH-WHEN-RUSH YES)
	(current-age-time > 300)
=>				 ;then
 	(build market)
;	(chat-local-to-self "build MARKET")
)

(defrule
 	(current-age >= feudal-age)
	(building-type-count-total market < 1)
 	(can-build market)
	(or
		(civ-selected aztec)
		(civ-selected mayan)
	)
	(goal RUSH-CONTROL CASTLE)
=>
 	(build market)
;	(chat-local-to-self "build MARKET")
)

(defrule
 	(building-type-count-total market < 1)
 	(can-build market)
	(current-age >= castle-age)
	(building-type-count-total town-center > 2)
	(wood-amount > 300)
=>				 ;then
 	(build market)
;	(chat-local-to-self "build MARKET")
)


#load-if-defined DEFEND-WONDER
(defrule
	(building-type-count-total monastery < 1)
 	(can-build monastery)
=>
 	(build monastery)
;	(chat-local-to-self "build MONASTERY")
)
#end-if

#load-if-defined WONDER-RACE
(defrule
	(building-type-count-total monastery < 1)
 	(current-age == castle-age)
 	(can-build monastery)
=>
 	(build monastery)
;	(chat-local-to-self "build MONASTERY")
)

(defrule
 	(current-age == castle-age)
 	(building-type-count-total university < 1)
 	(can-build university)
=>				 ;then
 	(build university)
;	(chat-local-to-self "build UNIVERSITY")
)
#end-if

#load-if-defined DEATH-MATCH
(defrule
	(building-type-count-total town-center > 1)
 	(current-age >= castle-age)
 	(building-type-count-total university < 1)
 	(can-build university)
	(not (current-age == post-imperial-age))
=>
 	(build university)
;	(chat-local-to-self "build UNIVERSITY")
)
#end-if


#load-if-not-defined DEATH-MATCH
#load-if-not-defined DEFEND-WONDER
#load-if-not-defined WONDER-RACE
(defrule
	(or
		(building-type-count-total town-center > 2)
		(wood-amount > 600)
	)
	(not (research-available ri-hand-cart))
 	(current-age >= castle-age)
 	(building-type-count-total university < 1)
	(building-type-count-total monastery > 0)
 	(can-build university)
	(goal RUSH-CONTROL NO)
	(not (current-age == post-imperial-age))
=>
 	(build university)
;	(chat-local-to-self "build UNIVERSITY")
)

(defrule
	(building-type-count-total town-center > 1)
	(not (research-available ri-hand-cart))
	(building-type-count-total monastery < 1)
	(can-build monastery)
	(or
		(goal RUSH-CONTROL NO)
		(wood-amount > 400)
	)
=>
	(build monastery)
;	(chat-local-to-self "build MONASTERY")
)

(defrule
	(building-type-count-total monastery < 2)
	(not (research-available ri-hand-cart))
	(building-type-count-total town-center > 2)
	(can-build monastery)
	(or
		(goal RUSH-CONTROL NO)
		(wood-amount > 400)
	)
=>
	(build monastery)
;	(chat-local-to-self "build MONASTERY")
)
#end-if
#end-if
#end-if

;************************************************************************************************************
;************************************************************************************************************
;MILITARY BUILDINGS



#load-if-not-defined WONDER-RACE
;***********************************************
;***********************************************



;BARRACKS
;*******************************************************************
;*******************************************************************



;Rule54: don't build barracks if don't need any

(defrule
	(true)
=>
	(set-goal ABLE-TO-BUILD-BARRACKS NO)
)

(defrule
	(or
		(or
			(or
				(or
					(or
						(goal PRIMARY-COMBAT-UNIT INFANTRY)
						(goal SECONDARY-COMBAT-UNIT INFANTRY)
					)
					(goal PRIMARY-COMBAT-UNIT EAGLEMAN)
				)
				(goal SECONDARY-COMBAT-UNIT EAGLEMAN)
			)
			(goal PRIMARY-COMBAT-UNIT SPEARMAN)
		)
		(goal SECONDARY-COMBAT-UNIT SPEARMAN)
	)
=>
	(set-goal ABLE-TO-BUILD-BARRACKS YES)
)

(defrule
	(or
		(goal PRIMARY-COMBAT-UNIT INFANTRY1)
		(goal SECONDARY-COMBAT-UNIT INFANTRY1)
	)
=>
	(set-goal ABLE-TO-BUILD-BARRACKS YES)
)


;build the first BARRACKS
(defrule
	(current-age >= feudal-age)
	(can-build barracks)
	(building-type-count-total barracks < 1)
	(not (research-available ri-wheel-barrow))
	(not (research-available ri-double-bit-axe))
	(not (research-available ri-horse-collar))
=>
	(build barracks)
;	(chat-local-to-self "build first BARRACKS")
)

(defrule
	(current-age == feudal-age)
	(can-build barracks)
	(building-type-count-total barracks < barracks-building-feudal)
	(goal ABLE-TO-BUILD-BARRACKS YES)
	(wood-amount > 600)
=>
	(build barracks)
;	(chat-local-to-self "build BARRACKS")
)

(defrule
	(can-build barracks)
	(or
		(and
			(building-type-count-total barracks < barracks-building-castle)
			(current-age == castle-age)
		)
		(and
			(building-type-count-total barracks < barracks-building-imperial)
			(current-age == imperial-age)
		)
	)
	(goal ABLE-TO-BUILD-BARRACKS YES)
	(wood-amount > 400)
	(not (research-available ri-hand-cart))
	(not (research-available ri-bow-saw))
	(not (research-available ri-heavy-plow))
	(building-type-count-total town-center > 1)
=>
	(build barracks)
;	(chat-local-to-self "build BARRACKS")
)

(defrule
	(current-age == dark-age)
 	(goal ADVANCING-TO-AGE FEUDAL-AGE)
 	(can-build barracks)
	(building-type-count-total barracks < 1)
=>
 	(build barracks)
;	(chat-local-to-self "build BARRACKS")
)

(defrule
 	(current-age == feudal-age)
 	(can-build barracks)
	(or
		(building-type-count-total barracks < 1)
		(and
			(building-type-count-total barracks < 2)
			(wood-amount > 400)
		)
	)
	(or
		(goal RUSH-CONTROL FEUDAL)
		(goal RUSH-CONTROL RUSHING)
	)
	(not (research-available ri-wheel-barrow))
	(not (research-available ri-double-bit-axe))
	(not (research-available ri-horse-collar))
=>
 	(build barracks)
;	(chat-local-to-self "build BARRACKS")
)

(defrule
	(current-age >= feudal-age)
 	(can-build barracks)
	(building-type-count-total barracks < 4)
	(or
		(building-type-count-total archery-range > 0)
		(building-type-count-total market > 0)
	)
	(or
		(goal RUSH-CONTROL CASTLE)
		(goal RUSH-CONTROL RUSHING)
	)
	(goal PRIMARY-COMBAT-UNIT EAGLEMAN)
=>
 	(build barracks)
;	(chat-local-to-self "build BARRACKS")
)

(defrule
 	(current-age >= castle-age)
	(or
		(and
			(building-type-count-total barracks < 2)
			(or
				(wood-amount > 450)
				(building-type-count-total town-center > 1)
			)
		)
		(building-type-count-total barracks < 1)
	)
 	(can-build barracks)
	(goal ABLE-TO-BUILD-BARRACKS YES)
=>
 	(build barracks)
;	(chat-local-to-self "build BARRACKS")
)

(defrule
 	(current-age >= castle-age)
	(building-type-count-total barracks < barracks-building-imperial)
	(or
		(wood-amount > 450)
		(building-type-count-total town-center > 2)
	)
	(goal ABLE-TO-BUILD-ARCHERY-RANGE YES)
	(building-type-count-total archery-range >= 2)
 	(can-build barracks)
	(goal ABLE-TO-BUILD-BARRACKS YES)
=>
 	(build barracks)
;	(chat-local-to-self "build BARRACKS")
)

(defrule
 	(current-age >= castle-age)
	(building-type-count-total barracks < barracks-building-imperial)
	(or
		(wood-amount > 450)
		(building-type-count-total town-center > 2)
	)
	(goal ABLE-TO-BUILD-STABLE YES)
	(building-type-count-total stable >= 2)
 	(can-build barracks)
	(goal ABLE-TO-BUILD-BARRACKS YES)
	(wood-amount > 300)
=>
 	(build barracks)
;	(chat-local-to-self "build BARRACKS")
)

(defrule
 	(current-age >= castle-age)
	(building-type-count-total barracks < barracks-building-imperial)
	(wood-amount > 550)
	(building-type-count-total town-center > 2)
 	(can-build barracks)
	(goal ABLE-TO-BUILD-BARRACKS YES)
=>
 	(build barracks)
;	(chat-local-to-self "build BARRACKS")
)

#load-if-defined DEATH-MATCH
;***********************************************
;***********************************************

;Rule69: build BARRACKS, in death-match games keep at least five handy (<= moderate)
(defrule
 	(building-type-count-total barracks < 5)
 	(can-build barracks)
	(wood-amount > 700)
=>
 	(build barracks)
;	(chat-local-to-self "build BARRACKS")
)

;***********************************************
;***********************************************
#end-if		;if game-type == death-match



;*******************************************************************
;*******************************************************************
;STABLE



;Rule82: don't build stable if we don't need any

(defrule
	(true)
=>
	(set-goal ABLE-TO-BUILD-STABLE NO)
)

(defrule
	(or
		(or
			(goal PRIMARY-COMBAT-UNIT LCAVALRY)
			(goal PRIMARY-COMBAT-UNIT KNIGHT)
		)
		(goal PRIMARY-COMBAT-UNIT CAMEL)
	)
=>
	(set-goal ABLE-TO-BUILD-STABLE YES)
)

(defrule
	(or
		(or
			(goal SECONDARY-COMBAT-UNIT LCAVALRY)
			(goal SECONDARY-COMBAT-UNIT KNIGHT)
		)
		(goal SECONDARY-COMBAT-UNIT CAMEL)
	)
=>
	(set-goal ABLE-TO-BUILD-STABLE YES)
)

;build STABLES in feudal-age

(defrule
	(current-age == feudal-age)
	(can-build stable)
	(or
		(and
			(building-type-count-total stable < stable-building-feudal)
			(wood-amount > 400)
		)
		(building-type-count-total stable < 1)
	)
	(goal ABLE-TO-BUILD-STABLE YES)
=>
	(build stable)
;	(chat-local-to-self "build STABLE")
)

;build a second stable when advancing to the castle age to spam knights
;quickly in case we get under attack
(defrule
	(can-build stable)
	(building-type-count-total stable < 2)
	(or
		(and
			(goal ADVANCING-TO-AGE CASTLE-AGE)
			(current-age == feudal-age)
		)
		(current-age >= castle-age)
	)
=>
	(build stable)
;	(chat-local-to-self "build STABLE")
)

(defrule
	(current-age == castle-age)
	(can-build stable)
	(or
		(and
			(building-type-count-total stable < stable-building-castle)
			(and
				(wood-amount > 400)
				(not (research-available ri-hand-cart))
			)
		)
		(building-type-count-total stable < 1)
	)
	(goal ABLE-TO-BUILD-STABLE YES)
=>
	(build stable)
;	(chat-local-to-self "build STABLE")
)

(defrule
	(current-age == imperial-age)
	(can-build stable)
	(or
		(and
			(building-type-count-total stable < stable-building-imperial)
			(and
				(wood-amount > 400)
				(not (research-available ri-hand-cart))
			)
		)
		(building-type-count-total stable < 1)
	)
	(goal ABLE-TO-BUILD-STABLE YES)
=>
	(build stable)
;	(chat-local-to-self "build STABLE")
)


;build STABLES in feudal-age

(defrule
 	(current-age == feudal-age)
	(building-type-count-total stable < 1)
	(building-type-count-total archery-range < 1)
 	(can-build stable)
	(not (goal ABLE-TO-BUILD-ARCHERY-RANGE YES))
	(or
		(goal RUSH-CONTROL CASTLE)
		(goal RUSH-CONTROL NO)
	)
	(or
		(and
			(not (research-available ri-wheel-barrow))
			(and
				(not (research-available ri-double-bit-axe))
				(not (research-available ri-horse-collar))
			)
		)
		(wood-amount > 500)
	)
=>
 	(build stable)
;	(chat-local-to-self "build STABLE")
)

(defrule
 	(current-age == feudal-age)
	(building-type-count-total stable < 1)
 	(can-build stable)
	(goal RUSH-CONTROL CASTLE)
	(goal ABLE-TO-BUILD-STABLE YES)
	(or
		(and
			(not (research-available ri-wheel-barrow))
			(and
				(not (research-available ri-double-bit-axe))
				(not (research-available ri-horse-collar))
			)
		)
		(wood-amount > 500)
	)
=>
 	(build stable)
;	(chat-local-to-self "build STABLE")
)

(defrule
 	(current-age == feudal-age)
	(building-type-count-total stable < 1)
 	(can-build stable)
	(goal RUSH-CONTROL CASTLE)
	(goal ABLE-TO-BUILD-STABLE YES)
	(wood-amount > 250)
	(goal PRIMARY-COMBAT-UNIT KNIGHT)
	(or
		(and
			(not (research-available ri-wheel-barrow))
			(and
				(not (research-available ri-double-bit-axe))
				(not (research-available ri-horse-collar))
			)
		)
		(wood-amount > 500)
	)
=>
 	(build stable)
;	(chat-local-to-self "build STABLE")
)

(defrule
	(current-age >= castle-age)
	(goal RUSH-CONTROL RUSHING)
	(building-type-count-total stable < 2)
	(can-build stable)
	(wood-amount > 250)
	(goal ABLE-TO-TRAIN-UNITS YES)
	(goal ABLE-TO-TRAIN-LAND-UNITS YES)
	(goal PRIMARY-COMBAT-UNIT KNIGHT)
=>
	(build stable)
;	(chat-local-to-self "build STABLE")
)

(defrule
 	(current-age >= castle-age)
	(or
		(and
			(building-type-count-total stable < 2)
			(or
				(wood-amount > 450)
				(building-type-count-total town-center > 1)
			)
		)
		(building-type-count-total stable < 1)
	)
 	(can-build stable)
	(goal ABLE-TO-BUILD-STABLE YES)
=>
 	(build stable)
;	(chat-local-to-self "build STABLE")
)

(defrule
 	(current-age >= castle-age)
	(building-type-count-total stable < stable-building-imperial)
	(or
		(wood-amount > 450)
		(building-type-count-total town-center > 1)
	)
	(not (research-available ri-hand-cart))
 	(can-build stable)
	(goal ABLE-TO-BUILD-STABLE YES)
	(wood-amount > 300)
=>
 	(build stable)
;	(chat-local-to-self "build STABLE")
)

#load-if-defined DEATH-MATCH
;***********************************************
;***********************************************

;Rule93: build STABLE, in death-match games keep at least five handy (<= moderate)
(defrule
 	(building-type-count-total stable < 5)
 	(can-build stable)
	(wood-amount > 700)
=>
 	(build stable)
;	(chat-local-to-self "build STABLE")
)

;***********************************************
;***********************************************
#end-if		;if game-type == death-match



;*******************************************************************
;*******************************************************************
;ARCHERY-RANGE



;Rule70: don't build archery-range if we don't need any

(defrule
	(true)
=>
	(set-goal ABLE-TO-BUILD-ARCHERY-RANGE NO)
)

(defrule
	(or
		(or
			(goal PRIMARY-COMBAT-UNIT CROSSBOW)
			(goal PRIMARY-COMBAT-UNIT CAVARCHER)
		)
		(goal PRIMARY-COMBAT-UNIT SKIRMISHER)
	)
=>
	(set-goal ABLE-TO-BUILD-ARCHERY-RANGE YES)
)

(defrule
	(or
		(or
			(goal SECONDARY-COMBAT-UNIT CROSSBOW)
			(goal SECONDARY-COMBAT-UNIT CAVARCHER)
		)
		(goal SECONDARY-COMBAT-UNIT SKIRMISHER)
	)
=>
	(set-goal ABLE-TO-BUILD-ARCHERY-RANGE YES)
)


;ARCHERY-RANGE in the feudal age

(defrule
 	(current-age == feudal-age)
 	(can-build archery-range)
	(or
		(building-type-count-total archery-range < 1)
		(and
			(building-type-count-total archery-range < 2)
			(wood-amount > 400)
		)
	)
	(or
		(goal RUSH-CONTROL FEUDAL)
		(goal RUSH-CONTROL RUSHING)
	)
	(not (research-available ri-wheel-barrow))
	(not (research-available ri-double-bit-axe))
	(not (research-available ri-horse-collar))
=>
 	(build archery-range)
;	(chat-local-to-self "build ARCHERY-RANGE")
)

(defrule
	(current-age == feudal-age)
	(can-build archery-range)
	(or
		(and
			(building-type-count-total archery-range < archery-range-building-feudal)
			(wood-amount > 600)
		)
		(building-type-count-total archery-range < 1)
	)
	(goal ABLE-TO-BUILD-ARCHERY-RANGE YES)
	(not (research-available ri-wheel-barrow))
	(not (research-available ri-double-bit-axe))
	(not (research-available ri-horse-collar))
=>
	(build archery-range)
;	(chat-local-to-self "build ARCHERY-RANGE")
)

(defrule
 	(current-age == feudal-age)
	(building-type-count-total archery-range < 1)
 	(can-build archery-range)
	(or
		(civ-selected aztec)
		(civ-selected mayan)
	)
	(not (research-available ri-wheel-barrow))
	(not (research-available ri-double-bit-axe))
	(not (research-available ri-horse-collar))
=>
 	(build archery-range)
;	(chat-local-to-self "build ARCHERY-RANGE")
)

(defrule
	(current-age == castle-age)
	(can-build archery-range)
	(or
		(and
			(building-type-count-total archery-range < archery-range-building-castle)
			(and
				(wood-amount > 600)
				(not (research-available ri-hand-cart))
			)
		)
		(building-type-count-total archery-range < 1)
	)
	(goal ABLE-TO-BUILD-ARCHERY-RANGE YES)
	(not (research-available ri-hand-cart))
	(not (research-available ri-bow-saw))
	(not (research-available ri-heavy-plow))
=>
	(build archery-range)
;	(chat-local-to-self "build ARCHERY-RANGE")
)

(defrule
	(current-age == imperial-age)
	(can-build archery-range)
	(or
		(and
			(building-type-count-total archery-range < archery-range-building-imperial)
			(and
				(wood-amount > 600)
				(not (research-available ri-hand-cart))
			)
		)
		(building-type-count-total archery-range < 1)
	)
	(goal ABLE-TO-BUILD-ARCHERY-RANGE YES)
	(not (research-available ri-hand-cart))
	(not (research-available ri-bow-saw))
	(not (research-available ri-heavy-plow))
=>
	(build archery-range)
;	(chat-local-to-self "build ARCHERY-RANGE")
)

;ARCHERY-RANGES in feudal-age if we do a flush

(defrule
 	(current-age == feudal-age)
	(not (goal RUSH-CONTROL NO))
	(not (goal RUSH-CONTROL CASTLE))
	(or
		(and
			(and
				(wood-amount > 500)
				(building-type-count-total archery-range < 2)
			)
			(goal ABLE-TO-RESEARCH-WHEN-RUSH YES)
		)
		(building-type-count-total archery-range < 1)
	)
 	(can-build archery-range)
	(not (research-available ri-wheel-barrow))
	(not (research-available ri-double-bit-axe))
	(not (research-available ri-horse-collar))
=>
 	(build archery-range)
;	(chat-local-to-self "build ARCHERY-RANGE")
)

(defrule
 	(current-age == feudal-age)
	(building-type-count-total archery-range < 1)
 	(can-build archery-range)
	(goal ABLE-TO-BUILD-ARCHERY-RANGE YES)
	(not (research-available ri-wheel-barrow))
	(not (research-available ri-double-bit-axe))
	(not (research-available ri-horse-collar))
=>
 	(build archery-range)
;	(chat-local-to-self "build ARCHERY-RANGE")
)

(defrule
 	(current-age >= feudal-age)
	(building-type-count-total archery-range < 1)
 	(can-build archery-range)
	(or
		(civ-selected aztec)
		(civ-selected mayan)
	)
	(goal RUSH-CONTROL NO)
	(not (research-available ri-wheel-barrow))
	(not (research-available ri-double-bit-axe))
	(not (research-available ri-horse-collar))
=>
 	(build archery-range)
;	(chat-local-to-self "build ARCHERY-RANGE")
)

(defrule
 	(current-age >= castle-age)
	(or
		(and
			(building-type-count-total archery-range < 2)
			(or
				(wood-amount > 450)
				(building-type-count-total town-center > 1)
			)
		)
		(building-type-count-total archery-range < 1)
	)
 	(can-build archery-range)
	(goal ABLE-TO-BUILD-ARCHERY-RANGE YES)
	(not (research-available ri-hand-cart))
	(not (research-available ri-bow-saw))
	(not (research-available ri-heavy-plow))
=>
 	(build archery-range)
;	(chat-local-to-self "build ARCHERY-RANGE")
)

(defrule
 	(current-age >= castle-age)
	(building-type-count-total archery-range < archery-range-building-imperial)
	(or
		(wood-amount > 450)
		(building-type-count-total town-center > 2)
	)
	(not (research-available ri-hand-cart))
 	(can-build archery-range)
	(goal ABLE-TO-BUILD-ARCHERY-RANGE YES)
	(wood-amount > 300)
=>
 	(build archery-range)
;	(chat-local-to-self "build ARCHERY-RANGE")
)

#load-if-defined DEATH-MATCH
;***********************************************
;***********************************************

;ARCHERY-RANGE, in death-match games keep at least five handy (<= moderate)
(defrule
 	(building-type-count-total archery-range < 5)
 	(can-build archery-range)
	(wood-amount > 700)
=>
 	(build archery-range)
;	(chat-local-to-self "build ARCHERY-RANGE")
)
;***********************************************
;***********************************************
#end-if		;if game-type == death-match



;*******************************************************************
;*******************************************************************
;SIEGE-WORKSHOP

(defrule
 	(current-age >= castle-age)
 	(can-build siege-workshop)
	(not (goal PRIMARY-COMBAT-SIEGE-UNIT NO))
	(nand
		(goal PRIMARY-COMBAT-SIEGE-UNIT TREBUCHET)
		(building-type-count-total castle > 0)
	)
	(or
		(building-type-count-total siege-workshop < 1)
		(and
			(building-type-count-total siege-workshop < siege-workshop-building-imperial)
			(wood-amount > 500)
		)
	)
	(wood-amount > 350)
	(building-type-count-total town-center > 2)
	(building-type-count-total monastery > 0)
	(building-type-count-total university > 0)
=>
 	(build siege-workshop)
;	(chat-local-to-self "build SIEGE-WORKSHOP")
)

(defrule
 	(current-age >= castle-age)
 	(can-build siege-workshop)
	(not (goal SECONDARY-COMBAT-SIEGE-UNIT NO))
	(nand
		(goal SECONDARY-COMBAT-SIEGE-UNIT TREBUCHET)
		(building-type-count-total castle > 0)
	)
	(or
		(building-type-count-total siege-workshop < 1)
		(and
			(building-type-count-total siege-workshop < siege-workshop-building-imperial)
			(wood-amount > 500)
		)
	)
	(wood-amount > 350)
	(building-type-count-total town-center > 2)
	(building-type-count-total monastery > 0)
	(building-type-count-total university > 0)
=>
 	(build siege-workshop)
;	(chat-local-to-self "build SIEGE-WORKSHOP")
)

#load-if-defined DEATH-MATCH
;***********************************************
;***********************************************

;Rule106: build SIEGE-WORKSHOP, in death-match games keep at least two handy
(defrule
 	(building-type-count-total siege-workshop < 2)
 	(can-build siege-workshop)
	(wood-amount > 700)
=>
 	(build siege-workshop)
;	(chat-local-to-self "build SIEGE-WORKSHOP")
)
;***********************************************
#end-if		;if game-type == death-match



;************************************************************************************************************
;************************************************************************************************************
;DEFENDING BUILDINGS



;WALLS
;***********************************************
;***********************************************



;Rule107: define the WALL-PLACEMENT

(defrule
	(not (map-type fortress))
	(not (map-type arena))
	(difficulty <= easy)
=>
	(enable-wall-placement 2)
	(disable-self)
)



#load-if-not-defined DEFEND-WONDER
;***********************************************
;***********************************************

;Rule113: build the WALL

(defrule
	(or
		(map-type black-forest)
		(map-type yucatan)
	)
	(difficulty <= easy)
	(can-build-wall 2 stone-wall-line)
	(wall-completed-percentage 2 < 100)
	(current-age == imperial-age)
=>
	(build-wall 2 stone-wall-line)
)

(defrule
	(wall-completed-percentage 2 > 0)
 	(current-age > dark-age)
 	(building-type-count-total gate < 7)
 	(can-build-gate-with-escrow 2)
=>
	(build-gate 2)
)

(defrule
	(wall-completed-percentage 2 == 100)
	(building-type-count-total gate < 7)
=>
	(delete-building stone-wall-line)
)
#end-if		;if not game-type == defend-the-wonder
;***********************************************
;***********************************************



;CASTLES
;***********************************************
;***********************************************



;Rule117: build CASTLES all the numbers here a scaled to the difficulty level; if we do not want to sling-shot through the castle-age

(defrule
	(or
		(and
 			(current-age == castle-age)
 			(building-type-count-total castle < castle-building-castle)
		)
		(and
 			(current-age == imperial-age)
 			(building-type-count-total castle < castle-building-imperial)
		)
	)
 	(can-build castle)
=>
 	(build castle)
;	(chat-local-to-self "build CASTLE")
)
;***********************************************
;***********************************************



;BOMBARD-TOWERS
(defrule
 	(current-age >= imperial-age)
 	(or
		(building-type-count-total bombard-tower < 5)
		(stone-amount > 900)
	)
 	(can-build bombard-tower)
	(or
		(building-type-count-total castle >= 2)
		(death-match-game)
	)
=>
 	(build bombard-tower)
;	(chat-local-to-self "build BOMBARD-TOWER")
)


#load-if-defined DEATH-MATCH

;Rule120: build WATCH-TOWERS (feudal-age) in deathmatch-games

(defrule
 	(current-age == feudal-age)
 	(building-type-count-total watch-tower < watch-tower-building-feudal)
 	(can-build watch-tower)
=>
 	(build watch-tower)
;	(chat-local-to-self "build TOWER")
)

#end-if


#load-if-not-defined GOTHIC-CIV
#load-if-not-defined HUN-CIV

;Rule121: build WATCH-TOWERS all the numbers here are scaled to the difficulty level part 2 (castle-age)

(defrule
 	(current-age == castle-age)
 	(building-type-count-total watch-tower < watch-tower-building-castle)
 	(can-build watch-tower)
	(building-type-count-total castle >= 3)
	(building-type-count-total town-center >= 3)
=>			;then
 	(build watch-tower)
;	(chat-local-to-self "build TOWER")
)

(defrule
 	(current-age == imperial-age)
 	(building-type-count-total watch-tower < watch-tower-building-imperial)
 	(can-build watch-tower)
	(or
		(building-type-count-total castle >= 2)
		(death-match-game)
	)
	(not (can-build bombard-tower))
	(building-type-count-total town-center >= 3)
=>
 	(build watch-tower)
;	(chat-local-to-self "build TOWER")
)

#end-if		;if not civ == gothic
#end-if		;if not civ == hun



;TOWN-CENTERS
;***********************************************
;***********************************************


(defrule
 	(current-age == castle-age)
 	(building-type-count-total town-center < 2)
 	(food-amount >= 200)
 	(can-build-with-escrow town-center)
 	(not (research-available ri-bow-saw))
=>
	(release-escrow wood)
	(release-escrow stone)
 	(build town-center)
;	(chat-local-to-self "build TOWN-CENTER")
)

(defrule
 	(current-age == castle-age)
 	(building-type-count-total town-center < town-center-building-castle)
 	(can-build-with-escrow town-center)
 	(not (research-available ri-bow-saw))
 	(not (research-available ri-hand-cart))
 	(not (research-available ri-heavy-plow))
=>
	(release-escrow wood)
	(release-escrow stone)
 	(build town-center)
;	(chat-local-to-self "build TOWN-CENTER")
)

(defrule
 	(current-age == imperial-age)
 	(building-type-count-total town-center < town-center-building-imperial)
 	(can-build-with-escrow town-center)
	(or
		(goal RUSH-CONTROL NO)
		(or
			(goal ABLE-TO-RESEARCH-WHEN-RUSH YES)
			(wood-amount > 400)
		)
	)
=>
	(release-escrow wood)
	(release-escrow stone)
 	(build town-center)
;	(chat-local-to-self "build TOWN-CENTER")
)
;***********************************************
#end-if		;if not game type == wonder race



;************************************************************************************************************
;************************************************************************************************************
;DOCKS



#load-if-not-defined WONDER-RACE
;***********************************************
;***********************************************



;Rule130: build first dock

(defrule
	(or
		(or
			(goal MAP-KIND RIVERS-MAP)
			(goal MAP-KIND ISLAND-MAP)
		)
		(goal MAP-KIND WATER-MAP)
	)
	(building-type-count-total dock == 0)
	(building-type-count-total barracks > 0)
	(building-type-count-total archery-range > 0)
	(building-type-count-total blacksmith > 0)
	(can-build dock)
	(building-type-count-total lumber-camp >= 1)
	(or
		(goal ADVANCING-TO-AGE CASTLE-AGE)
		(current-age >= castle-age)
	)
=>
	(build dock)
;	(chat-local-to-self "build DOCK")
)

(defrule
	(or
		(or
			(goal MAP-KIND RIVERS-MAP)
			(goal MAP-KIND ISLAND-MAP)
		)
		(goal MAP-KIND WATER-MAP)
	)
	(building-type-count-total dock == 0)
	(building-type-count-total barracks > 0)
	(building-type-count-total stable > 0)
	(building-type-count-total blacksmith > 0)
	(can-build dock)
	(building-type-count-total lumber-camp >= 1)
	(or
		(goal ADVANCING-TO-AGE CASTLE-AGE)
		(current-age >= castle-age)
	)
=>
	(build dock)
;	(chat-local-to-self "build DOCK")
)

(defrule
	(or
		(or
			(goal MAP-KIND RIVERS-MAP)
			(goal MAP-KIND ISLAND-MAP)
		)
		(goal MAP-KIND WATER-MAP)
	)
	(building-type-count-total dock < 2)
	(building-type-count-total barracks > 0)
	(building-type-count-total archery-range > 0)
	(building-type-count-total blacksmith > 0)
	(can-build dock)
	(wood-amount > 200)
	(building-type-count-total lumber-camp >= 1)
	(or
		(building-type-count-total town-center > 1)
		(wood-amount > 400)
	)
=>
	(build dock)
;	(chat-local-to-self "build DOCK")
)

(defrule
	(or
		(or
			(goal MAP-KIND RIVERS-MAP)
			(goal MAP-KIND ISLAND-MAP)
		)
		(goal MAP-KIND WATER-MAP)
	)
	(building-type-count-total dock < 2)
	(building-type-count-total barracks > 0)
	(building-type-count-total stable > 0)
	(building-type-count-total blacksmith > 0)
	(can-build dock)
	(wood-amount > 200)
	(building-type-count-total lumber-camp >= 1)
	(or
		(building-type-count-total town-center > 1)
		(wood-amount > 400)
	)
=>
	(build dock)
;	(chat-local-to-self "build DOCK")
)

(defrule
	(or
		(or
			(goal MAP-KIND RIVERS-MAP)
			(goal MAP-KIND ISLAND-MAP)
		)
		(goal MAP-KIND WATER-MAP)
	)
	(current-age >= castle-age)
	(wood-amount > 300)
	(building-type-count-total dock < 3)
	(can-build dock)
	(building-type-count-total lumber-camp >= 1)
	(or
		(building-type-count-total town-center > 1)
		(wood-amount > 400)
	)
=>
	(build dock)
;	(chat-local-to-self "build DOCK")
)

(defrule
	(or
		(goal MAP-KIND WATER-MAP)
		(goal MAP-KIND ISLAND-MAP)
	)
	(current-age == imperial-age)
	(wood-amount > 300)
	(difficulty <= moderate)
	(building-type-count-total dock < 4)
	(can-build dock)
	(building-type-count-total lumber-camp >= 1)
	(or
		(building-type-count-total town-center > 1)
		(wood-amount > 400)
	)
=>
	(build dock)
;	(chat-local-to-self "build DOCK")
)

(defrule
	(or
		(goal MAP-KIND WATER-MAP)
		(goal MAP-KIND ISLAND-MAP)
	)
	(current-age == imperial-age)
	(wood-amount > 1000)
	(difficulty <= hard)
	(building-type-count-total dock < 6)
	(can-build dock)
	(building-type-count-total lumber-camp >= 1)
=>
	(build dock)
;	(chat-local-to-self "build DOCK")
)

#load-if-defined DEFEND-WONDER
;***********************************************
;***********************************************

;Rule134: build docks when we play "defend-the-wonder" (number of docks scaled to the water size)

(defrule
	(or
		(and
			(goal MAP-KIND WATER-MAP)
			(building-type-count-total dock < 2)
		)
		(and
			(not (goal MAP-KIND LAND-MAP))
			(building-type-count-total dock == 0)
		)
	)
	(can-build dock)
	(building-type-count-total lumber-camp >= 1)
=>
	(build dock)
;	(chat-local-to-self "build DOCK")
)
;***********************************************
#end-if		;if game type == defend-the-wonder

;***********************************************
;***********************************************
#else		;if not game-type == wonder-race
;***********************************************
;***********************************************

;Rule135: build a dock when we have got a lumber camp

(defrule
	(or
		(or
			(goal MAP-KIND RIVERS-MAP)
			(goal MAP-KIND ISLAND-MAP)
		)
		(goal MAP-KIND WATER-MAP)
	)
	(building-type-count-total dock == 0)
	(building-type-count-total lumber-camp > 0)
	(can-build dock)
=>
	(build dock)
)

(defrule
	(or
		(or
			(goal MAP-KIND RIVERS-MAP)
			(goal MAP-KIND ISLAND-MAP)
		)
		(goal MAP-KIND WATER-MAP)
	)
	(current-age >= feudal-age)
	(wood-amount > 400)
	(building-type-count-total dock < 2)
	(can-build dock)
=>
	(build dock)
)

(defrule
	(or
		(or
			(goal MAP-KIND RIVERS-MAP)
			(goal MAP-KIND ISLAND-MAP)
		)
		(goal MAP-KIND WATER-MAP)
	)
	(current-age >= castle-age)
	(wood-amount > 1000)
	(building-type-count-total dock < 2)
	(can-build dock)
=>
	(build dock)
)
;************************************************************************************************************
;BUILD THE WONDER IN WONDER RACE GAMES



;Rule137 and 138: build the wonder in wonder race games

(defrule
	(true)
=>
	(set-goal WONDER-ATTEMPT YES)
)

(defrule
	(goal WONDER-ATTEMPT YES)
	(can-build wonder)
=>
	(set-strategic-number sn-percent-civilian-builders 95)
	(set-strategic-number sn-cap-civilian-builders 40)
	(set-strategic-number sn-percent-civilian-gatherers 5)
	(release-escrow wood)
	(release-escrow gold)
	(release-escrow stone)
	(build wonder)
	(set-goal WONDER-ATTEMPT POSSIBLE)
	(chat-to-player every-ally "39 Okay, I have got the resources to build a wonder!")
)
;***********************************************
#end-if		;if game-type == wonder race



;************************************************************************************************************
;************************************************************************************************************
;FORWARD BUILDINGS


;===============================
;ATTACK-PERSONALITY: ATTACKER
;===============================



#load-if-not-defined WONDER-RACE


;**********************************
;**********************************
;BOMBARD-TOWES AND CASTLES


#load-if-defined KOREAN-CIV

;Rule19: if we are the koreans we can do a bombard-tower-rush

(defrule
	(can-build bombard-tower)
	(building-type-count-total castle >= 2)
	(building-type-count-total market > 0)
	(building-type-count-total blacksmith > 0)
	(goal ATTACK-PERSONALITY ATTACKER)
=>
	(build-forward bombard-tower)
)

#end-if		;if civ == korean



#load-if-defined TURKISH-CIV

;Rule19: if we are the koreans we can do a bombard-tower-rush

(defrule
	(can-build bombard-tower)
	(building-type-count-total castle >= 2)
	(building-type-count-total market > 0)
	(building-type-count-total blacksmith > 0)
	(goal ATTACK-PERSONALITY ATTACKER)
=>
	(build-forward bombard-tower)
)

#end-if		;if civ == turkish


;FORWARD-CASTLE

(defrule
	(goal ATTACK-PERSONALITY ATTACKER)
	(current-age >= castle-age)
	(players-current-age any-enemy < castle-age)
	(can-build castle)
=>
	(build-forward castle)
;	(chat-local-to-self "build FORWARD-CASTLE")
)

(defrule
	(goal ATTACK-PERSONALITY ATTACKER)
	(current-age >= castle-age)
	(or
		(goal RUSH-CONTROL RUSHING)
		(and
			(building-type-count-total castle > 1)
			(building-type-count-total castle < 14)
		)
	)
	(can-build castle)
	(building-type-count-total market > 0)
	(building-type-count-total blacksmith > 0)
=>
	(build-forward castle)
;	(chat-local-to-self "build FORWARD-CASTLE")
)


;***********************************
;***********************************
;BARRACKS


(defrule
	(goal ATTACK-PERSONALITY ATTACKER)
	(can-build barracks)
	(goal ABLE-TO-BUILD-BARRACKS YES)
	(wood-amount > 500)
	(building-type-count-total market > 0)
	(building-type-count-total blacksmith > 0)
	(building-type-count-total barracks >= 3)
=>
	(build-forward barracks)
)

;***********************************
;***********************************
;ARCHERY-RANGE


(defrule
	(goal ATTACK-PERSONALITY ATTACKER)
	(can-build archery-range)
	(goal ABLE-TO-BUILD-ARCHERY-RANGE YES)
	(wood-amount > 500)
	(building-type-count-total market > 0)
	(building-type-count-total blacksmith > 0)
	(building-type-count-total archery-range >= 3)
=>
	(build-forward archery-range)
)

;***********************************
;***********************************
;STABLE


(defrule
	(goal ATTACK-PERSONALITY ATTACKER)
	(can-build stable)
	(goal ABLE-TO-BUILD-STABLE YES)
	(wood-amount > 500)
	(building-type-count-total market > 0)
	(building-type-count-total blacksmith > 0)
	(building-type-count-total stable >= 3)
=>
	(build-forward stable)
)

;==================
;FORWARD BUILDERS
;==================



;Rule27: initialisation of the number of forward builders

(defrule
	(true)
=>
	(set-strategic-number sn-number-forward-builders 0)
	(disable-self)
)


#end-if		;if not game type == wonder race
