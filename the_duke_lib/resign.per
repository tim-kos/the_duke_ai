;======================================================================
;		STUFF FOR AI-RESIGNING
;======================================================================



#load-if-not-defined DIFFICULTY-MODERATE
#load-if-not-defined DIFFICULTY-HARD
#load-if-not-defined DIFFICULTY-HARDEST

;Rule1: RESIGN IF EASIEST OR EASY

(defrule
	(not (goal RUSH-CONTROL RESIGN))
	(game-time > 300)
	(soldier-count < 5)
	(unit-type-count villager < 5)
	(nor
		(hold-relics)
		(hold-koh-ruin)
	)
	(not (can-train villager))
=>
	(chat-to-player every-enemy "40 You're well prepared to play on the next difficulty-level!")
	(chat-to-player every-enemy "-Tim Koschuetzki, The Duke-AI - developer")
	(set-goal RUSH-CONTROL RESIGN)
)

#end-if		;if not difficulty type == moderate
#end-if		;if not difficulty type == hard
#end-if		;if not difficulty type == hardest



#load-if-defined DIFFICULTY-MODERATE

;Rule2: RESIGN IF MODERATE

(defrule
	(not (goal RUSH-CONTROL RESIGN))
	(game-time > 300)
	(building-type-count wonder < 1)
	(soldier-count < 4)
	(unit-type-count villager < 4)
	(nor
		(hold-relics)
		(hold-koh-ruin)
	)
	(not (can-train villager))
=>
	(chat-to-player every-enemy "40 I think that was not so difficult, wasn't it?")
	(chat-to-player every-enemy "Try to play on difficulty level hard!")
	(chat-to-player every-enemy "-Tim Koschuetzki, The Duke-AI - developer")
	(set-goal RUSH-CONTROL RESIGN)
)

#end-if		;if difficulty type == moderate



#load-if-defined DIFFICULTY-HARD

;Rule3: RESIGN IF HARD

(defrule
	(not (goal RUSH-CONTROL RESIGN))
	(game-time > 300)
	(building-type-count wonder < 1)
	(soldier-count < 3)
	(unit-type-count cannon-galleon-line == 0)
	(unit-type-count villager <= 2)
	(nor
		(hold-relics)
		(hold-koh-ruin)
	)
	(not (can-train villager))
=>
	(chat-to-player every-enemy "40 If you didn't cheat you're well prepared")
	(chat-to-player every-enemy "to play on the most challenging difficulty-level!")
	(chat-to-player every-enemy "-Tim Koschuetzki, The Duke-AI - developer")
	(set-goal RUSH-CONTROL RESIGN)
)

#end-if		;if difficulty type == hard



#load-if-defined DIFFICULTY-HARDEST

;Rule4: RESIGN IF HARDEST

(defrule
	(not (goal RUSH-CONTROL RESIGN))
	(game-time > 300)
	(building-type-count wonder < 1)
	(soldier-count < 3)
	(unit-type-count cannon-galleon-line == 0)
	(unit-type-count villager <= 2)
	(nor
		(hold-relics)
		(hold-koh-ruin)
	)
	(not (can-train villager))
=>
	(chat-to-player every-enemy "40 The Duke-AI can't be harder than this!")
	(chat-to-player every-enemy "If you didn't cheat you're a really good AoE-player!")
	(chat-to-player every-enemy "-Tim Koschuetzki, The Duke-AI - developer")
	(set-goal RUSH-CONTROL RESIGN)
)

#end-if		;if difficulty type == hardest



;Rule5: TRIBUTE TO COMPUTER ALLIES IF WE'LL RESIGN

(defrule
	(goal RUSH-CONTROL RESIGN)
	(players-population any-ally > 10)
	(difficulty <= moderate)
=>
	(release-escrow food)
	(release-escrow wood)
	(release-escrow gold)
	(release-escrow stone)
 	(tribute-to-player this-any-ally food 20000)
	(tribute-to-player this-any-ally wood 20000)
	(tribute-to-player this-any-ally gold 20000)
	(tribute-to-player this-any-ally stone 20000)
)

(defrule
	(goal RUSH-CONTROL RESIGN)
	(stance-toward any-human ally)
	(difficulty <= moderate)
=>
	(release-escrow food)
	(release-escrow wood)
	(release-escrow gold)
	(release-escrow stone)
 	(tribute-to-player this-any-human food 20000)
	(tribute-to-player this-any-human wood 20000)
	(tribute-to-player this-any-human gold 20000)
	(tribute-to-player this-any-human stone 20000)
)

(defrule
	(goal RUSH-CONTROL RESIGN)
=>
	(delete-unit villager)
)

(defrule
	(goal RUSH-CONTROL RESIGN)
=>
	(delete-building town-center)

	(delete-building monastery)
	(delete-building university)
	(delete-building blacksmith)

	(delete-building stable)
	(delete-building barracks)
	(delete-building archery-range)
	(delete-building siege-workshop)
)

(defrule
	(goal RUSH-CONTROL RESIGN)
	(not (stance-toward any-human ally))
=>
 	(delete-building watch-tower)
	(delete-building guard-tower)
	(delete-building keep)

	(delete-building bombard-tower)

	(delete-building castle)
)
;that the player can't read it

(defrule
	(goal RUSH-CONTROL RESIGN)
=>
	(enable-timer T-RESIGN 10)
	(disable-self)
)

(defrule
	(goal RUSH-CONTROL RESIGN)
	(timer-triggered T-RESIGN)
	(building-type-count blacksmith == 0)
	(building-type-count university == 0)
	(building-type-count monastery == 0)
	(building-type-count town-center == 0)
	(building-type-count barracks == 0)
	(building-type-count archery-range == 0)
	(building-type-count stable == 0)
	(building-type-count siege-workshop == 0)
=>
	(disable-timer T-RESIGN)
	(set-goal RUSH-CONTROL RESIGN-NOW)
)

(defrule
	(goal RUSH-CONTROL RESIGN-NOW)
=>
	(resign)
	(chat-local-to-self "Resign")
	(chat-to-all-using-range 22300  22)
	(chat-to-all-using-id 22322)
		;"No wonder thou wert victorious! I shalt abdicate."
	(disable-self)
)
